import Mathlib.Analysis.GodefroyLipschitz.Annexe
import Mathlib.Analysis.GodefroyLipschitz.Extension
import Mathlib.MeasureTheory.Measure.OpenPos

open Real NNReal Set Filter Topology FiniteDimensional Metric Module Submodule
open WeakDual

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]

theorem unique1 [FiniteDimensional â„ E] {x : E} (nx : â€–xâ€– = 1) (hx : DifferentiableAt â„ (â€–Â·â€–) x)
    {Ï† : E â†’ â„} (hÏ† : LipschitzWith 1 Ï†) (Ï†_eq : âˆ€ t : â„, Ï† (t â€¢ x) = t) :
    Ï† = fderiv â„ (â€–Â·â€–) x := by
  ext y
  have this t (ht : t â‰  0) : 1 = |t * (Ï† y) - t * (Ï† (((Ï† y) + 1 / t) â€¢ x))| := by
    simp [Ï†_eq, mul_comm, mul_add, ht]
  have this (t : â„) : 1 â‰¤ â€–x - t â€¢ (y - (Ï† y) â€¢ x)â€– := by
    obtain rfl | ht := eq_or_ne t 0
    Â· rw [zero_smul, sub_zero, nx]
    Â· calc
        1 â‰¤ |t| * â€–y - (Ï† y + 1 / t) â€¢ xâ€– := by
          nth_rw 1 [this t ht, â† mul_sub, abs_mul, â† norm_eq_abs (_ - _)]
          rw [_root_.mul_le_mul_left (abs_pos.2 ht)]
          simpa using hÏ†.norm_sub_le _ _
        _ = â€–x - t â€¢ (y - (Ï† y) â€¢ x)â€– := by
          rw [â† norm_eq_abs, â† norm_smul, â† norm_neg, smul_sub, smul_smul]
          congr
          field_simp
          module
  have min : IsLocalMin (fun t : â„ â†¦ â€–x - t â€¢ (y - (Ï† y) â€¢ x)â€–) 0 := by
    simp [IsLocalMin, IsMinFilter, nx, this]
  have : deriv (fun t : â„ â†¦ â€–x - t â€¢ (y - (Ï† y) â€¢ x)â€–) 0 = - fderiv â„ (â€–Â·â€–) x (y - (Ï† y) â€¢ x) := by
    change deriv ((â€–Â·â€–) âˆ˜ _) _ = _
    rw [fderiv_comp_deriv]
    Â· rw [deriv_const_sub, deriv_smul_const] <;> simp
    Â· simpa
    Â· simp
  rw [min.deriv_eq_zero, map_sub, _root_.map_smul, fderiv_norm_self hx, nx] at this
  simp only [smul_eq_mul, mul_one, neg_sub] at this
  exact sub_eq_zero.1 this.symm

theorem Filter.Tendsto.fderiv_norm_tendsto_norm {Î¹ : Type*} {â„± : Filter Î¹}
    {x : Î¹ â†’ E} (hd : âˆ€ i, DifferentiableAt â„ (â€–Â·â€–) (x i))
    {z : E} (ht : Tendsto x â„± (ğ“ z)) :
    Tendsto (fun n â†¦ fderiv â„ (â€–Â·â€–) (x n) z) â„± (ğ“ â€–zâ€–) := by
  obtain hE | _ := subsingleton_or_nontrivial E
  Â· rw [subsingleton_iff.1 hE z 0]
    simpa using tendsto_const_nhds
  Â· have aux1 : Tendsto (fun n â†¦ fderiv â„ (â€–Â·â€–) (x n) (x n)) â„± (ğ“ â€–zâ€–) := by
      simp_rw [fun n â†¦ fderiv_norm_self (hd n)]
      exact (continuous_norm.tendsto z).comp ht
    apply tendsto_of_tendsto_of_dist aux1
    simp_rw [dist_eq_norm, â† map_sub]
    apply squeeze_zero (fun n â†¦ norm_nonneg _) (fun n â†¦ ContinuousLinearMap.le_opNorm _ _)
    simp_rw [fun n â†¦ norm_fderiv_norm (hd n), one_mul]
    exact tendsto_iff_norm_sub_tendsto_zero.1 ht

private lemma eq_of_abs_le_sub_eq {a b c : â„} (ha : |a| â‰¤ c) (hb : |b| â‰¤ c) (h : a - b = 2 * c) :
    a = c âˆ§ b = -c := by
  by_contra this
  obtain ha' | hb' := Classical.not_and_iff_or_not_not.1 this
  Â· linarith [(abs_le.1 hb).1, lt_of_le_of_ne (abs_le.1 ha).2 ha']
  Â· linarith [(abs_le.1 ha).2, lt_of_le_of_ne (abs_le.1 hb).1 (ne_eq .. â–¸ hb').symm]

variable {E F : Type*} [NormedAddGroup E] [NormedAddGroup F] in
theorem Isometry.map_norm_sub {Ï† : E â†’ F} (hÏ† : Isometry Ï†) (x y : E) :
    â€–Ï† x - Ï† yâ€– = â€–x - yâ€– := by
  rw [â† dist_eq_norm, hÏ†.dist_eq, dist_eq_norm]

open ContinuousLinearMap in
private lemma jsp {f : F â†’L[â„] â„} {a b : â„} {Ï† : â„ â†’ F} (hÏ† : Isometry Ï†) (Ï†z : Ï† 0 = 0)
    (nf : â€–fâ€– = 1) (hfa : f (Ï† a) = a) (hb : b âˆˆ Icc 0 a) : f (Ï† b) = b := by
  apply le_antisymm
  Â· refine le_trans (le_norm_self _) ?_
    convert f.le_opNorm _ using 1
    rw [nf, hÏ†.norm_map_of_map_zero Ï†z, one_mul, norm_of_nonneg hb.1]
  Â· nth_rw 1 [â† neg_le_neg_iff, â† add_le_add_iff_left a, â† hfa]
    simp_rw [â† sub_eq_add_neg, â† map_sub]
    refine le_trans (le_norm_self _) ?_
    convert f.le_opNorm _ using 1
    rw [hÏ†.map_norm_sub, nf, one_mul, norm_of_nonneg (by linarith [hb.2])]

open ContinuousLinearMap in
private lemma jsp2 {f : F â†’L[â„] â„} {a b : â„} {Ï† : â„ â†’ F} (hÏ† : Isometry Ï†) (Ï†z : Ï† 0 = 0)
    (nf : â€–fâ€– = 1) (hfa : f (Ï† a) = a) (hb : b âˆˆ Icc a 0) : f (Ï† b) = b := by
  apply le_antisymm
  Â· rw [â† sub_add_cancel (f (Ï† b)) (f (Ï† a)), â† map_sub, â† le_sub_iff_add_le, hfa]
    refine le_trans (le_norm_self _) ?_
    convert f.le_opNorm _ using 1
    rw [hÏ†.map_norm_sub, nf, one_mul, norm_of_nonneg (by linarith [hb.1])]
  Â· rw [â† neg_le_neg_iff]
    refine le_trans (le_norm_self _) (norm_neg (f _) â–¸ ?_)
    convert f.le_opNorm _ using 1
    rw [nf, hÏ†.norm_map_of_map_zero Ï†z, one_mul, norm_of_nonpos hb.2]

open ContinuousLinearMap in
theorem exists_inverse {Ï† : â„ â†’ F} (hÏ† : Isometry Ï†) (Ï†z : Ï† 0 = 0) :
    âˆƒ (f : F â†’L[â„] â„), â€–fâ€– = 1 âˆ§ âˆ€ t : â„, f (Ï† t) = t := by
  have _ : Nontrivial F := by
    refine nontrivial_iff.2 âŸ¨Ï† 1, 0, ?_âŸ©
    rw [â† norm_ne_zero_iff, hÏ†.norm_map_of_map_zero Ï†z, norm_one]
    norm_num
  have (k : â„•) :
      âˆƒ f : WeakDual â„ F, â€–toNormedDual fâ€– = 1 âˆ§ âˆ€ s : â„, s âˆˆ Icc (-k : â„) k â†’ f (Ï† s) = s := by
    obtain âŸ¨f, nf, hfâŸ© : âˆƒ f : F â†’L[â„] â„, â€–fâ€– = 1 âˆ§ f ((Ï† k) - (Ï† (-k))) = 2 * k := by
      have nk : â€–(Ï† k) - (Ï† (-k))â€– = 2 * k := by
        rw [hÏ†.map_norm_sub, norm_eq_abs, sub_neg_eq_add, two_mul, abs_eq_self.2 (by positivity)]
      obtain âŸ¨f, nf, hfkâŸ© := exists_dual_vector'  â„ ((Ï† k) - (Ï† (-k)))
      simp only [RCLike.ofReal_real_eq_id, id_eq] at hfk
      exact âŸ¨f, nf, by rw [hfk, nk]âŸ©
    refine âŸ¨f, nf, fun s âŸ¨hs1, hs2âŸ© â†¦ ?_âŸ©
    have âŸ¨h1, h2âŸ© : f (Ï† k) = k âˆ§ f (Ï† (-k)) = -k := by
      refine eq_of_abs_le_sub_eq ?_ ?_ (by rw [â† map_sub, hf]) <;> rw [â† norm_eq_abs]
      Â· convert f.le_opNorm (Ï† k)
        rw [nf, one_mul, hÏ†.norm_map_of_map_zero Ï†z, norm_of_nonneg (by positivity)]
      Â· convert f.le_opNorm (Ï† (-k))
        rw [nf, one_mul, hÏ†.norm_map_of_map_zero Ï†z, norm_of_nonpos (by simp), neg_neg]
    obtain hs | hs := le_total s 0
    Â· exact jsp2 hÏ† Ï†z nf h2 âŸ¨hs1, hsâŸ©
    Â· exact jsp hÏ† Ï†z nf h1 âŸ¨hs, hs2âŸ©
  choose! f nf hf using this
  obtain âŸ¨g, ng, hgâŸ© : âˆƒ g âˆˆ toNormedDual â»Â¹' closedBall 0 1, MapClusterPt g atTop f := by
    have aux : atTop.map f â‰¤ ğ“Ÿ (toNormedDual â»Â¹' closedBall 0 1) := by
      rw [le_principal_iff, â† eventually_mem_set, eventually_map]
      exact Eventually.of_forall fun n â†¦ by simp [-coe_toNormedDual, nf]
    exact (WeakDual.isCompact_closedBall _ _ _).exists_clusterPt aux
  have (t : â„) : g (Ï† t) = t := by
    obtain âŸ¨Ïˆ, hÏˆ, hâŸ© := TopologicalSpace.FirstCountableTopology.tendsto_subseq <|
      hg.tendsto_comp ((eval_continuous (Ï† t)).tendsto g)
    have : Tendsto (fun n â†¦ f (Ïˆ n) (Ï† t)) atTop (ğ“ t) := by
      refine tendsto_atTop_of_eventually_const (iâ‚€ := âŒˆ|t|âŒ‰â‚Š) fun i hi â†¦ hf _ _ ?_
      replace hi : âŒˆ|t|âŒ‰â‚Š â‰¤ Ïˆ i := hi.trans hÏˆ.le_apply
      rw [mem_Icc]
      rwa [Nat.ceil_le, abs_le] at hi
    exact tendsto_nhds_unique h this
  refine âŸ¨toNormedDual g, le_antisymm ?_ ?_, thisâŸ©
  Â· rwa [mem_preimage, mem_closedBall, dist_zero_right] at ng
  Â· apply le_opNorm_of' (x := Ï† 1)
    Â· rw [hÏ†.norm_map_of_map_zero Ï†z, norm_one]
    Â· rw [toNormedDual_apply, this, norm_one]

theorem norm_normalize {x : E} (hx : x â‰  0) : â€–(1 / â€–xâ€–) â€¢ xâ€– = 1 := by
  rw [norm_smul, norm_div, norm_one, norm_norm, one_div_mul_cancel (norm_ne_zero_iff.2 hx)]

theorem ne_zero_of_differentiableAt_norm [Nontrivial E]
    {x : E} (h : DifferentiableAt â„ (â€–Â·â€–) x) : x â‰  0 :=
  fun hx â†¦ (not_differentiableAt_norm_zero E (hx â–¸ h)).elim

theorem exists_inverse' [FiniteDimensional â„ E] [Nontrivial E]
    {Ï† : E â†’ F} (hÏ† : Isometry Ï†) (Ï†z : Ï† 0 = 0)
    (hdÏ† : Dense (span â„ (range Ï†) : Set F)) :
    âˆƒ (f : F â†’L[â„] E), â€–fâ€– = 1 âˆ§ f âˆ˜ Ï† = id := by
  -- For any `x` with norm `1` there exists a continuous linear form `fâ‚“`
  -- such that for any `t : â„`, `fâ‚“ (Ï† (t â€¢ x)) = t`.
  have (x : E) (nx : â€–xâ€– = 1) : âˆƒ f : F â†’L[â„] â„, â€–fâ€– = 1 âˆ§ âˆ€ t : â„, f (Ï† (t â€¢ x)) = t := by
    refine exists_inverse (Isometry.of_dist_eq fun xâ‚ xâ‚‚ â†¦ ?_) (by simpa)
    rw [hÏ†.dist_eq, dist_eq_norm, â† sub_smul, norm_smul, nx, mul_one, dist_eq_norm]
  choose! f nf hf using this
  -- The set of points where the norm is differentiable is dense
  have dense_diff : Dense {x : E | DifferentiableAt â„ (â€–Â·â€–) x} := dense_differentiableAt_norm
  let s : Set (E â†’â‚—[â„] â„) :=
    {f : E â†’â‚—[â„] â„ | âˆƒ x : E, â€–xâ€– = 1 âˆ§ DifferentiableAt â„ (â€–Â·â€–) x âˆ§ f = fderiv â„ (â€–Â·â€–) x}
  have aux3 (z : E) (hz : z â‰  0) : âˆƒ f âˆˆ s, f z â‰  0 := by
    obtain âŸ¨u, hu, htuâŸ© := mem_closure_iff_seq_limit.1 (dense_diff z)
    have := (htu.fderiv_norm_tendsto_norm hu).eventually_ne (norm_ne_zero_iff.2 hz)
    obtain âŸ¨N, hNâŸ© := eventually_atTop.1 this
    have h : u N â‰  0 := ne_zero_of_differentiableAt_norm (hu N)
    refine âŸ¨fderiv â„ (â€–Â·â€–) ((1 / â€–u Nâ€–) â€¢ u N), âŸ¨(1 / â€–u Nâ€–) â€¢ u N, norm_normalize h, ?_, rflâŸ©, ?_âŸ©
    Â· exact (differentiableAt_norm_smul (one_div_ne_zero (norm_ne_zero_iff.2 h))).1 (hu N)
    Â· rw [fderiv_norm_smul_pos (one_div_pos.2 <| norm_pos_iff.2 h)]
      exact hN N le_rfl
  let b := (Basis.ofSpan (span_eq_top_of_ne_zero aux3))
  choose y ny dy hy using fun i â†¦ Basis.ofSpan_subset (span_eq_top_of_ne_zero aux3) âŸ¨i, rflâŸ©
  classical
  let c := (b.dualBasis).map (evalEquiv â„ E).symm
  have b_map_c i j : b i (c j) = if i = j then 1 else 0 := by
    simp only [Basis.map_apply, apply_evalEquiv_symm_apply, Basis.dualBasis_apply_self, b, c]
  let T : F â†’L[â„] E :=
    { toFun := fun z â†¦ âˆ‘ i, (f (y i) z) â€¢ (c i)
      map_add' := fun _ â†¦ by simp [Finset.sum_add_distrib, add_smul]
      map_smul' := fun _ â†¦ by simp [Finset.smul_sum, smul_smul]
      cont := by fun_prop }
  use T
  have lipfÏ† {x : E} (nx : â€–xâ€– = 1) : LipschitzWith 1 ((f x) âˆ˜ Ï†) := by
    convert (f x).lipschitz.comp hÏ†.lipschitz
    rw [â† norm_toNNReal, nf x nx, mul_one, toNNReal_one]
  have fÏ†_eq {x : E} (nx : â€–xâ€– = 1) (hx : DifferentiableAt â„ (â€–Â·â€–) x) :=
    unique1 nx hx (lipfÏ† nx) (hf x nx)
  have TÏ† x : T (Ï† x) = x := by
    have aux2 i x : f (y i) (Ï† x) = b i x := by
      convert congrFun (fÏ†_eq (ny i) (dy i)) x using 1
      exact DFunLike.congr_fun (hy i) x
    simp only [ContinuousLinearMap.coe_mk', LinearMap.coe_mk, AddHom.coe_mk, aux2, T]
    let g : E â†’â‚—[â„] E :=
      { toFun := fun y â†¦ âˆ‘ i, (b i y) â€¢ (c i)
        map_add' := fun _ â†¦ by simp [Finset.sum_add_distrib, add_smul]
        map_smul' := fun _ â†¦ by simp [Finset.smul_sum, smul_smul] }
    have : g = LinearMap.id := c.ext fun i â†¦ by simp [g, b_map_c]
    exact DFunLike.congr_fun this x
  refine âŸ¨le_antisymm (T.opNorm_le_bound (by norm_num) fun y â†¦ ?_) ?_, funext TÏ†âŸ©
  Â· have prim {x : E} (nx : â€–xâ€– = 1) (hx : DifferentiableAt â„ (â€–Â·â€–) x) :
        f x = (fderiv â„ (â€–Â·â€–) x) âˆ˜L T := by
      apply ContinuousLinearMap.ext_on hdÏ†
      rintro - âŸ¨y, rflâŸ©
      simp only [ContinuousLinearMap.coe_comp', Function.comp_apply, TÏ†]
      exact congrFun (fÏ†_eq nx hx) y
    obtain âŸ¨u, hu, htuâŸ© := mem_closure_iff_seq_limit.1 <| dense_diff (T y)
    have := htu.fderiv_norm_tendsto_norm hu
    have unez n : u n â‰  0 := fun h â†¦ not_differentiableAt_norm_zero E (h â–¸ hu n)
    have obv n : 1 / â€–u nâ€– > 0 := one_div_pos.2 <| norm_pos_iff.2 <| unez n
    simp_rw [â† fun n â†¦ fderiv_norm_smul_pos (x := u n) (obv n)] at this
    refine le_of_tendsto' this fun n â†¦ ?_
    have : fderiv â„ (â€–Â·â€–) ((1 / â€–u nâ€–) â€¢ (u n)) (T y) = f ((1 / â€–u nâ€–) â€¢ (u n)) y :=
      DFunLike.congr_fun (prim (norm_normalize (unez n))
        ((differentiableAt_norm_smul (obv n).ne.symm).1 (hu n))).symm y
    rw [this]
    calc
      f ((1 / â€–u nâ€–) â€¢ (u n)) y â‰¤ â€–f ((1 / â€–u nâ€–) â€¢ (u n)) yâ€– := le_norm_self _
      _ â‰¤ â€–f ((1 / â€–u nâ€–) â€¢ (u n))â€– * â€–yâ€– := ContinuousLinearMap.le_opNorm _ y
      _ = 1 * â€–yâ€– := by rw [nf _ (norm_normalize (unez n))]
  Â· obtain âŸ¨x, hxâŸ© := NormedSpace.exists_lt_norm â„ E 0
    apply le_of_mul_le_mul_right _ hx
    nth_rw 1 [â† TÏ† x]
    rw [â† hÏ†.norm_map_of_map_zero Ï†z x, one_mul]
    exact T.le_opNorm _

theorem exists_inverse'' [CompleteSpace E] [Nontrivial E]
    (Ï† : E â†’ F) (hÏ† : Isometry Ï†) (Ï†z : Ï† 0 = 0)
    (hdÏ† : Dense (Submodule.span â„ (range Ï†) : Set F)) :
    âˆƒ (f : F â†’L[â„] E), â€–fâ€– = 1 âˆ§ f âˆ˜ Ï† = id := by
  let A : Submodule â„ E â†’ Submodule â„ F := fun p â†¦ span â„ (Ï† '' p)
  have mA : Monotone A := fun p q hpq â†¦ span_mono (image_mono hpq)
  let Ïˆ : (p : Submodule â„ E) â†’ p â†’ A p := fun p x â†¦ âŸ¨Ï† x, subset_span âŸ¨x.1, x.2, rflâŸ©âŸ©
  have span_Ïˆ p : span â„ (range (Ïˆ p)) = âŠ¤ := by
    apply span_coe
    rintro âŸ¨-, hÏ†yâŸ© âŸ¨y, hy, rflâŸ©
    use âŸ¨y, hyâŸ©
  have hÏˆ p : Isometry (Ïˆ p) := Isometry.of_dist_eq fun x y â†¦ hÏ†.dist_eq _ _
  have Ïˆz p : Ïˆ p 0 = 0 := by simp [Ïˆ, Ï†z]
  have fini (p : Submodule â„ E) [hp : FiniteDimensional â„ p] :
      âˆƒ T : A p â†’â‚—[â„] E, (âˆ€ y, â€–T yâ€– â‰¤ 1 * â€–yâ€–) âˆ§ âˆ€ y : p, T (Ïˆ p y) = y := by
    obtain âŸ¨T, nT, hTâŸ© : âˆƒ T : A p â†’â‚—[â„] p, (âˆ€ y, â€–T yâ€– â‰¤ 1 * â€–yâ€–) âˆ§ âˆ€ y : p, T (Ïˆ p y) = y := by
      by_cases np : Nontrivial p
      Â· have : Dense (X := A p) (span â„ (range (Ïˆ p))) := by
          convert dense_univ
          rw [â† top_coe (R := â„)]
          exact congrArg _ (span_Ïˆ p)
        obtain âŸ¨T, nT, hTâŸ© := exists_inverse' (hÏˆ p) (Ïˆz p) this
        exact âŸ¨T, fun y â†¦ nT â–¸ T.le_opNorm y, fun y â†¦ congrFun hT yâŸ©
      Â· refine âŸ¨0, by simp, ?_âŸ©
        rw [not_nontrivial_iff_subsingleton] at np
        exact fun _ â†¦ Subsingleton.allEq _ _
    refine âŸ¨p.subtype âˆ˜â‚— T, fun y â†¦ ?_, fun y â†¦ ?_âŸ©
    Â· simpa using nT y
    Â· simpa using hT y
  choose! T' nT' hT' using fini
  let T (p : Submodule â„ E) [hp : FiniteDimensional â„ p] : F â†’â‚—.[â„] E := âŸ¨A p, T' pâŸ©
  have nT {p : Submodule â„ E} [hp : FiniteDimensional â„ p] (y : A p) : â€–T p yâ€– â‰¤ 1 * â€–yâ€– := nT' p y
  have hT {p : Submodule â„ E} [hp : FiniteDimensional â„ p] (y : p) : T p (Ïˆ p y) = y := hT' p y
  have monoT {p q : Submodule â„ E} [FiniteDimensional â„ p] [FiniteDimensional â„ q] (hpq : p â‰¤ q) :
      T p â‰¤ T q := by
    refine âŸ¨mA hpq, fun x y hxy â†¦ ?_âŸ©
    have : (T p).toFun = (T q).toFun âˆ˜â‚— (Submodule.inclusion (mA hpq)) := by
      refine LinearMap.ext_on_range (span_Ïˆ p) fun x â†¦ ?_
      simp only [LinearMap.coe_comp, Function.comp_apply]
      have : Submodule.inclusion (mA hpq) (Ïˆ p x) = Ïˆ q (Submodule.inclusion hpq x) := rfl
      rw [hT' p, this, hT' q]
      rfl
    change (T p).toFun _ = _
    rw [this]
    simp only [LinearMap.coe_comp, Function.comp_apply, LinearPMap.mk_apply, T]
    congr
    rw [â† Subtype.val_inj, â† hxy, Submodule.coe_inclusion]
  let c : Set (F â†’â‚—.[â„] E) := {f | âˆƒ (p : Submodule â„ E) (hp : FiniteDimensional â„ p), f = T p}
  have mem_c (p : Submodule â„ E) [FiniteDimensional â„ p] : T p âˆˆ c := âŸ¨p, inferInstance, rflâŸ©
  have mem_c' {f : F â†’â‚—.[â„] E} (hf : f âˆˆ c) : âˆƒ (p : Submodule â„ E) (_ : FiniteDimensional â„ p),
      f = T p := hf
  have Dc : DirectedOn (Â· â‰¤ Â·) c := by
    rintro - âŸ¨p, hp, rflâŸ© - âŸ¨q, hq, rflâŸ©
    exact âŸ¨T (p âŠ” q), mem_c _, monoT le_sup_left, monoT le_sup_rightâŸ©
  let S := LinearPMap.sSup c Dc
  have mem_domS (x : S.domain) : âˆƒ (y : F) (p : Submodule â„ E) (_ : FiniteDimensional â„ p),
      y âˆˆ A p âˆ§ x = y := by
    obtain âŸ¨y, hyâŸ© := x
    simp only [LinearPMap.sSup, S] at hy
    rw [Submodule.mem_sSup_of_directed] at hy
    Â· obtain âŸ¨-, âŸ¨-, âŸ¨p, hp, rflâŸ©, rflâŸ©, hyâŸ© := hy
      exact âŸ¨y, p, inferInstance, hy, rflâŸ©
    Â· exact âŸ¨A âŠ¥, T âŠ¥, mem_c âŠ¥, rflâŸ©
    Â· exact Monotone.directedOn LinearPMap.domain_mono.monotone Dc
  have S_eq {x : S.domain} {p : Submodule â„ E} [FiniteDimensional â„ p] (hx : x.1 âˆˆ A p) :
      S x = T p âŸ¨x, hxâŸ© := LinearPMap.sSup_apply Dc (mem_c p) âŸ¨x, hxâŸ©
  have dense_domS : Dense (S.domain : Set F) := by
    simp only [S, LinearPMap.sSup]
    apply hdÏ†.mono
    norm_cast
    rw [span_le]
    rintro - âŸ¨x, rflâŸ©
    apply Submodule.mem_sSup_of_mem (s := A (â„ âˆ™ x))
    Â· exact âŸ¨T (â„ âˆ™ x), mem_c _, rflâŸ©
    Â· exact subset_span âŸ¨x, mem_span_singleton_self x, rflâŸ©
  have hS x : â€–S xâ€– â‰¤ 1 * â€–xâ€– := by
    obtain âŸ¨y, p, _, hy, rflâŸ© := mem_domS x
    rw [S_eq hy]
    exact nT _
  have cS : UniformContinuous S := AddMonoidHomClass.uniformContinuous_of_bound _ _ hS
  let U := dense_extend dense_domS cS
  use U
  have main x : U (Ï† x) = x := by
    have h1 : Ï† x âˆˆ A (â„ âˆ™ x) := subset_span âŸ¨x, mem_span_singleton_self x, rflâŸ©
    have h2 : Ï† x âˆˆ S.domain := (LinearPMap.le_sSup Dc (mem_c (â„ âˆ™ x))).1 h1
    change U (âŸ¨Ï† x, h2âŸ© : S.domain) = x
    rw [dense_extend_eq, S_eq h1, hT âŸ¨x, mem_span_singleton_self xâŸ©]
  constructor
  Â· apply le_antisymm
    Â· exact norm_dense_extend _ _ (by norm_num) hS
    Â· obtain âŸ¨x, hxâŸ© := exists_norm_eq E zero_le_one
      apply le_opNorm_of' (x := Ï† x)
      Â· exact hx â–¸ hÏ†.norm_map_of_map_zero Ï†z x
      Â· rw [main, hx]
  Â· exact funext main
