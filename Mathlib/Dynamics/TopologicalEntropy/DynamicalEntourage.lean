/-
Copyright (c) 2024 Damien Thomine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Damien Thomine, Pietro Monticone
-/
import Mathlib.Order.Interval.Finset.Nat
import Mathlib.Topology.Constructions.SumProd
import Mathlib.Topology.UniformSpace.Defs
import Mathlib.Data.Nat.Lattice

/-!
# Dynamical entourages
Bowen-Dinaburg's definition of topological entropy of a transformation `T` in a metric space
`(X, d)` relies on the so-called dynamical balls. These balls are sets
`B (x, Œµ, n) = { y | ‚àÄ k < n, d(T^[k] x, T^[k] y) < Œµ }`.

We implement Bowen-Dinaburg's definitions in the more general context of uniform spaces. Dynamical
balls are replaced by what we call dynamical entourages. This file collects all general lemmas
about these objects.

## Main definitions
- `dynEntourage`: dynamical entourage associated with a given transformation `T`, entourage `U`
and time `n`.

## Tags
entropy

## TODO
Once #PR14718 has passed, add product of entourages.

In the context of (pseudo-e)metric spaces, relate the usual definition of dynamical balls with
these dynamical entourages.
-/

namespace Dynamics

open Prod Set Uniformity UniformSpace

variable {X : Type*}

/-- The dynamical entourage associated to a transformation `T`, entourage `U` and time `n`
is the set of points `(x, y)` such that `(T^[k] x, T^[k] y) ‚àà U` for all `k < n`, i.e.
which are `U`-close up to time `n`. -/
def dynEntourage (T : X ‚Üí X) (U : Set (X √ó X)) (n : ‚Ñï) : Set (X √ó X) :=
  ‚ãÇ k < n, (map T T)^[k] ‚Åª¬π' U

lemma dynEntourage_eq_inter_Ico (T : X ‚Üí X) (U : Set (X √ó X)) (n : ‚Ñï) :
    dynEntourage T U n = ‚ãÇ k : Ico 0 n, (map T T)^[k] ‚Åª¬π' U := by
  simp [dynEntourage]

lemma mem_dynEntourage {T : X ‚Üí X} {U : Set (X √ó X)} {n : ‚Ñï} {x y : X} :
    (x, y) ‚àà dynEntourage T U n ‚Üî ‚àÄ k < n, (T^[k] x, T^[k] y) ‚àà U := by
  simp [dynEntourage]

lemma mem_ball_dynEntourage {T : X ‚Üí X} {U : Set (X √ó X)} {n : ‚Ñï} {x y : X} :
    y ‚àà ball x (dynEntourage T U n) ‚Üî ‚àÄ k < n, T^[k] y ‚àà ball (T^[k] x) U := by
  simp only [ball, mem_preimage]; exact mem_dynEntourage

lemma dynEntourage_mem_uniformity [UniformSpace X] {T : X ‚Üí X} (h : UniformContinuous T)
    {U : Set (X √ó X)} (U_uni : U ‚àà ùì§ X) (n : ‚Ñï) :
    dynEntourage T U n ‚àà ùì§ X := by
  rw [dynEntourage_eq_inter_Ico T U n]
  induction n with
  | zero => simp
  | succ n ih =>
    simp only [iInter_coe_set, mem_Ico, Nat.zero_le, true_and] at ih ‚ä¢
    rw [Set.biInter_lt_succ]
    apply Filter.inter_mem ih
    rw [map_iterate T T n]
    exact uniformContinuous_def.1 (UniformContinuous.iterate T n h) U U_uni

lemma idRel_subset_dynEntourage (T : X ‚Üí X) {U : Set (X √ó X)} (h : idRel ‚äÜ U) (n : ‚Ñï) :
    idRel ‚äÜ (dynEntourage T U n) := by
  simp only [dynEntourage, map_iterate, subset_iInter_iff, idRel_subset, mem_preimage, map_apply]
  exact fun _ _ _ ‚Ü¶ h rfl

lemma _root_.IsSymmetricRel.dynEntourage (T : X ‚Üí X) {U : Set (X √ó X)}
    (h : IsSymmetricRel U) (n : ‚Ñï) :
    IsSymmetricRel (dynEntourage T U n) := by
  ext xy
  simp only [Dynamics.dynEntourage, map_iterate, mem_preimage, mem_iInter]
  refine forall‚ÇÇ_congr fun k _ ‚Ü¶ ?_
  exact map_apply' _ _ _ ‚ñ∏ IsSymmetricRel.mk_mem_comm h

@[deprecated (since := "2025-03-05")]
alias _root_.SymmetricRel.dynEntourage := _root_.IsSymmetricRel.dynEntourage

lemma dynEntourage_comp_subset (T : X ‚Üí X) (U V : Set (X √ó X)) (n : ‚Ñï) :
    (dynEntourage T U n) ‚óã (dynEntourage T V n) ‚äÜ dynEntourage T (U ‚óã V) n := by
  simp only [dynEntourage, map_iterate, subset_iInter_iff]
  intro k k_n xy xy_comp
  simp only [compRel, mem_iInter, mem_preimage, map_apply, mem_setOf_eq] at xy_comp ‚ä¢
  rcases xy_comp with ‚ü®z, hz1, hz2‚ü©
  exact mem_ball_comp (hz1 k k_n) (hz2 k k_n)

lemma _root_.isOpen.dynEntourage [TopologicalSpace X] {T : X ‚Üí X} (T_cont : Continuous T)
    {U : Set (X √ó X)} (U_open : IsOpen U) (n : ‚Ñï) :
    IsOpen (dynEntourage T U n) := by
  rw [dynEntourage_eq_inter_Ico T U n]
  refine isOpen_iInter_of_finite fun k ‚Ü¶ ?_
  exact U_open.preimage ((T_cont.prodMap T_cont).iterate k)

lemma dynEntourage_monotone (T : X ‚Üí X) (n : ‚Ñï) :
    Monotone (fun U : Set (X √ó X) ‚Ü¶ dynEntourage T U n) :=
  fun _ _ h ‚Ü¶ iInter‚ÇÇ_mono fun _ _ ‚Ü¶ preimage_mono h

lemma dynEntourage_antitone (T : X ‚Üí X) (U : Set (X √ó X)) :
    Antitone (fun n : ‚Ñï ‚Ü¶ dynEntourage T U n) :=
  fun m n m_n ‚Ü¶ iInter‚ÇÇ_mono' fun k k_m ‚Ü¶ by use k, lt_of_lt_of_le k_m m_n

@[simp]
lemma dynEntourage_zero {T : X ‚Üí X} {U : Set (X √ó X)} :
    dynEntourage T U 0 = univ := by simp [dynEntourage]

@[simp]
lemma dynEntourage_one {T : X ‚Üí X} {U : Set (X √ó X)} :
    dynEntourage T U 1 = U := by simp [dynEntourage]

@[simp]
lemma dynEntourage_univ {T : X ‚Üí X} {n : ‚Ñï} :
    dynEntourage T univ n = univ := by simp [dynEntourage]

lemma mem_ball_dynEntourage_comp (T : X ‚Üí X) (n : ‚Ñï) {U : Set (X √ó X)} (U_symm : IsSymmetricRel U)
    (x y : X) (h : (ball x (dynEntourage T U n) ‚à© ball y (dynEntourage T U n)).Nonempty) :
    x ‚àà ball y (dynEntourage T (U ‚óã U) n) := by
  rcases h with ‚ü®z, z_Bx, z_By‚ü©
  rw [mem_ball_symmetry (IsSymmetricRel.dynEntourage T U_symm n)] at z_Bx
  exact dynEntourage_comp_subset T U U n (mem_ball_comp z_By z_Bx)

lemma _root_.Function.Semiconj.preimage_dynEntourage {Y : Type*} {S : X ‚Üí X} {T : Y ‚Üí Y} {œÜ : X ‚Üí Y}
    (h : Function.Semiconj œÜ S T) (U : Set (Y √ó Y)) (n : ‚Ñï) :
    (map œÜ œÜ)‚Åª¬π' (dynEntourage T U n) = dynEntourage S ((map œÜ œÜ)‚Åª¬π' U) n := by
  rw [dynEntourage, preimage_iInter‚ÇÇ]
  refine iInter‚ÇÇ_congr fun k _ ‚Ü¶ ?_
  rw [‚Üê preimage_comp, ‚Üê preimage_comp, map_iterate S S k, map_iterate T T k, map_comp_map,
    map_comp_map, (Function.Semiconj.iterate_right h k).comp_eq]

end Dynamics
