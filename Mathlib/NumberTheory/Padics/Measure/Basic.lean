/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

import Mathlib.NumberTheory.Padics.MahlerBasis
import Mathlib.Topology.UniformSpace.CoveringProfinite

/-!
# Abstract measures on topological spaces

We define an "abstract measure" on `X`, with values in a normed ring `R`, to be an `R`-linear
functional on continuous maps `X ‚Üí R`. This is an important construction in p-adic analysis (where
the Iwasawa algebra is defined as the space of abstract measures on `‚Ñ§_[p]` with values in `‚Ñö_[p]`).
-/

open ContinuousMap

variable
    {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    {R : Type*} [NormedCommRing R]
    {E : Type*} [NormedAddCommGroup E] [Module R E] [ContinuousSMul R E]

attribute [local ext] DFunLike.ext -- why is this not set by default?

section Preliminaries

open scoped TensorProduct

/-- Pullback via a continuous map, as a continuous linear map on continuous functions. -/
@[simps apply]
def ContinuousMap.comapCLM (f : C(X, Y)) : C(Y, E) ‚ÜíL[R] C(X, E) where
  toFun g := g.comp f
  map_add' _ _ := add_comp _ _ f
  map_smul' _ _ := smul_comp _ _ f

/-- The natural bilinear map sending `f, g` to the function `(x, y) ‚Ü¶ f x * g y` on `X √ó Y`. -/
def ContinuousMap.prodMul : C(X, R) ‚Üí‚Çó[R] C(Y, R) ‚Üí‚Çó[R] C(X √ó Y, R) :=
  LinearMap.mk‚ÇÇ R (fun f g ‚Ü¶ (f.comp .fst) * (g.comp .snd))
    (fun f f' g ‚Ü¶ by ext; simp [add_mul])
    (fun r f g ‚Ü¶ by ext; simp [mul_assoc])
    (fun f g g' ‚Ü¶ by ext; simp [mul_add])
    (fun r f g ‚Ü¶ by ext; simp [mul_left_comm])

lemma ContinuousMap.prodMul_apply (f : C(X, R)) (g : C(Y, R)) (p : X √ó Y) :
    f.prodMul g p  = f p.1 * g p.2 := rfl

/-- Tensor product version of `ContinuousMap.prodMul`. -/
noncomputable def ContinuousMap.tensorHom : C(X, R) ‚äó[R] C(Y, R) ‚Üí C(X √ó Y, R) :=
  TensorProduct.lift ContinuousMap.prodMul

/-- Composition of continuous linear maps, as a linear map. Compare `LinearMap.lcomp`. -/
@[simps]
def ContinuousLinearMap.lcomp {U V : Type*} (W : Type*)
    [AddCommMonoid U] [Module R U] [TopologicalSpace U]
    [AddCommMonoid V] [Module R V] [TopologicalSpace V]
    [AddCommGroup W] [Module R W] [TopologicalSpace W]
    [IsTopologicalAddGroup W] [ContinuousSMul R W]
    (f : U ‚ÜíL[R] V) : (V ‚ÜíL[R] W) ‚Üí‚Çó[R] (U ‚ÜíL[R] W) where
  toFun l := l.comp f
  map_add' _ _ := by simp
  map_smul' _ _ := by simp

/-- Composition of continuous linear maps, as a bilinear map. Compare `LinearMap.llcomp`. -/
@[simps]
def ContinuousLinearMap.llcomp (U V W : Type*)
    [AddCommGroup U] [Module R U] [TopologicalSpace U]
    [AddCommGroup V] [Module R V] [TopologicalSpace V]
    [IsTopologicalAddGroup V] [ContinuousSMul R V]
    [AddCommGroup W] [Module R W] [TopologicalSpace W]
    [IsTopologicalAddGroup W] [ContinuousSMul R W] :
    (U ‚ÜíL[R] V) ‚Üí‚Çó[R] (V ‚ÜíL[R] W) ‚Üí‚Çó[R] (U ‚ÜíL[R] W) where
  toFun l := l.lcomp W
  map_add' _ _ := by ext; simp
  map_smul' _ _ := by ext; simp

end Preliminaries

section Defs

/-!
### Basic definitions
-/

variable (X R E) in
/--
The space of `E`-valued measures on `X`, i.e. continuous linear maps `C(X, R) ‚Üí E`. (The case
`R = E` is the most important case.)

This is a type synonym for `C(X, R) ‚ÜíL[R] E`, but we do not want it to inherit the default
(norm) topology.
-/
def AbstractMeasure := C(X, R) ‚ÜíL[R] E

scoped [AbstractMeasure] notation "D(" X ", " R ")" => AbstractMeasure X R R

end Defs

namespace AbstractMeasure

/-- Inherit `FunLike` structure from `C(G, R) ‚ÜíL[R] E`. -/
instance : FunLike (AbstractMeasure X R E) C(X, R) E :=
  inferInstanceAs (FunLike (C(X, R) ‚ÜíL[R] E) C(X, R) E)

/-- Inherit `ContinuousLinearMapClass` structure from `C(X, R) ‚ÜíL[R] E`. -/
instance : ContinuousLinearMapClass (AbstractMeasure X R E) R C(X, R) E :=
  inferInstanceAs (ContinuousLinearMapClass (C(X, R) ‚ÜíL[R] E) R C(X, R) E)

/-- Inherit `AddCommGroup` structure from `C(G, R) ‚ÜíL[R] E`. -/
instance : AddCommGroup (AbstractMeasure X R E) :=
  inferInstanceAs (AddCommGroup (C(X, R) ‚ÜíL[R] E))

/-- Inherit `R`-module structure from `C(G, R) ‚ÜíL[R] E`. -/
instance : Module R (AbstractMeasure X R E) :=
  inferInstanceAs (Module R (C(X, R) ‚ÜíL[R] E))

variable (R) in
/-- The Dirac measure, "evaluation at `x`". -/
def dirac (x : X) : D(X, R) :=
  ContinuousMap.evalCLM R x

@[simp] lemma dirac_apply (x : X) (f : C(X, R)) : dirac R x f = f x := rfl

@[simp]
lemma smul_apply (r : R) (Œº : AbstractMeasure X R E) (f : C(X, R)) : (r ‚Ä¢ Œº) f = r ‚Ä¢ Œº f :=
  rfl

omit [ContinuousSMul R E] in
@[simp]
lemma add_apply (Œº ŒΩ : AbstractMeasure X R E) (f : C(X, R)) : (Œº + ŒΩ) f = Œº f + ŒΩ f :=
  rfl

/-- Measures can be pushed forward (R-linearly) along continuous maps. -/
def map (f : C(X, Y)) : AbstractMeasure X R E ‚Üí‚Çó[R] AbstractMeasure Y R E where
  toFun Œº := Œº ‚àòL comapCLM f
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

@[simp] lemma map_apply (f : C(X, Y)) (Œº : AbstractMeasure X R E) (g : C(Y, R)) :
    map f Œº g = Œº (g.comp f) :=
  rfl

lemma map_dirac (f : C(X, Y)) (x : X) : map f (dirac R x) = dirac R (f x) := rfl

section Prod

/-!
### Product structure
-/

-- note we define `contractSnd` first, because `f.curry` only works one way round

/-- Integrate a measure on `Y` against a function on `X √ó Y`, giving a function on `X`. -/
def contractSnd : D(Y, R) ‚Üí‚Çó[R] C(X √ó Y, R) ‚Üí‚Çó[R] C(X, R) :=
  LinearMap.mk‚ÇÇ R (fun ŒΩ f ‚Ü¶ comp ŒΩ f.curry)
    (fun ŒΩ ŒΩ' f ‚Ü¶ by
      ext x
      simp only [comp_apply, ContinuousMap.coe_coe, add_apply, ContinuousMap.add_apply])
    (fun r ŒΩ f ‚Ü¶ by
      ext x
      simp only [comp_apply, ContinuousMap.coe_coe, smul_apply, smul_eq_mul, coe_smul, coe_comp,
        Pi.smul_apply, Function.comp_apply])
    (fun ŒΩ f f' ‚Ü¶ by
      ext x
      simp only [comp_apply, ContinuousMap.coe_coe, ContinuousMap.add_apply, ‚Üê map_add]
      rfl)
    (fun ŒΩ r f ‚Ü¶ by
      ext x
      simp only [comp_apply, ContinuousMap.coe_coe, ContinuousMap.smul_apply, ‚Üê map_smul]
      rfl)

/-- Integrate a measure on `X` against a function on `X √ó Y`, giving a function on `Y`. -/
def contractFst : D(X, R) ‚Üí‚Çó[R] C(X √ó Y, R) ‚Üí‚Çó[R] C(Y, R) :=
  (prodSwap.comapCLM.toLinearMap.lcomp R _).comp contractSnd

variable (Œº : D(X, R)) (ŒΩ : D(Y, R))

@[simp]
lemma contractFst_apply (f : C(X √ó Y, R)) (y : Y) :
    contractFst Œº f y = Œº ‚ü®fun x ‚Ü¶ f (x, y), by continuity‚ü© :=
  rfl

@[simp]
lemma contractSnd_apply (f : C(X √ó Y, R)) (x : X) :
    contractSnd ŒΩ f x = ŒΩ ‚ü®fun y ‚Ü¶ f (x, y), by continuity‚ü© :=
  rfl

lemma contractFst_dirac (x : X) (y : Y) (f : C(X √ó Y, R)) :
    contractFst (dirac R x) f y = f (x, y) :=
  rfl

lemma contractSnd_dirac (x : X) (y : Y) (f : C(X √ó Y, R)) :
    contractSnd (dirac R y) f x = f (x, y) :=
  rfl

section LocallyCompact

variable [LocallyCompactSpace X] [LocallyCompactSpace Y]

/-- `AbstractMeasure.contractSnd` bundled with continuity in the function argument. -/
def contractSndCLM : D(Y, R) ‚Üí‚Çó[R] C(X √ó Y, R) ‚ÜíL[R] C(X, R) where
  toFun ŒΩ := ‚ü®contractSnd ŒΩ, by
    refine continuous_of_continuous_uncurry _ (ŒΩ.continuous.comp ?_)
    apply continuous_of_continuous_uncurry
    rw [‚Üê (Homeomorph.prodAssoc C(X √ó Y, R) X Y).symm.comp_continuous_iff']
    exact ContinuousEval.continuous_eval‚ü©
  map_add' _ _ := ContinuousLinearMap.coe_injective.eq_iff.mp <| contractSnd.map_add _ _
  map_smul' _ _ := ContinuousLinearMap.coe_injective.eq_iff.mp <| contractSnd.map_smul _ _

lemma contractSndCLM_apply (f : C(X √ó Y, R)) (x : X) :
    contractSndCLM ŒΩ f x = ŒΩ ‚ü®fun y ‚Ü¶ f (x, y), by continuity‚ü© := rfl

/-- `AbstractMeasure.contractFst` bundled with continuity in the function argument. -/
def contractFstCLM : D(X, R) ‚Üí‚Çó[R] C(X √ó Y, R) ‚ÜíL[R] C(Y, R) :=
  (ContinuousMap.prodSwap.comapCLM.lcomp _).comp contractSndCLM

lemma contractFstCLM_apply (f : C(X √ó Y, R)) (y : Y) :
    contractFstCLM Œº f y = Œº ‚ü®fun x ‚Ü¶ f (x, y), by continuity‚ü© := rfl

/-- "Left-handed" version of the natural product map on distributions (acting on functions
as first integrating along `X`, and then integrating the result along `Y`). -/
def prodMk : D(X, R) ‚Üí‚Çó[R] D(Y, R) ‚Üí‚Çó[R] D(X √ó Y, R) :=
  (ContinuousLinearMap.llcomp _ _ R).comp contractFstCLM

@[simp] lemma prodMk_apply (f : C(X √ó Y, R)) :
  prodMk Œº ŒΩ f = ŒΩ (Œº.contractFst f) := rfl

/-- On functions of the form `(x, y) ‚Ü¶ f x * g y`, the measure `prodMk Œº ŒΩ` agrees with the
algebraic tensor product of `Œº` and `ŒΩ`. -/
lemma prodMk_prod_apply (f : C(X, R)) (g : C(Y, R)) :
    prodMk Œº ŒΩ ((f.comp .fst) * (g.comp .snd)) = Œº f * ŒΩ g := by
  simp only [‚Üê smul_eq_mul, prodMk_apply, ‚Üê map_smul]
  congr 1 with y
  simp_rw [contractFst_apply, ContinuousMap.smul_apply, smul_eq_mul, mul_comm (Œº f) (g y),
    ‚Üê smul_eq_mul, ‚Üê map_smul]
  congr 1 with x
  simp_rw [ContinuousMap.smul_apply, smul_eq_mul, mul_comm (g y) (f x)]
  rfl

/-- "Right-handed" version of the natural product map on distributions (acting on functions
as first integrating along `Y`, and then integrating the result along `X`). -/
def prodMk' : D(X, R) ‚Üí‚Çó[R] D(Y, R) ‚Üí‚Çó[R] D(X √ó Y, R) :=
  ((ContinuousLinearMap.llcomp _ _ R).comp contractSndCLM).flip

lemma prodMk'_apply (f : C(X √ó Y, R)) : (Œº.prodMk' ŒΩ) f = Œº (ŒΩ.contractSnd f) := rfl

lemma prodMk'_flip (f : C(X √ó Y, R)) :
    (Œº.prodMk' ŒΩ) f = (ŒΩ.prodMk Œº) (f.comp ContinuousMap.prodSwap) := rfl

lemma prodMk'_prod_apply (f : C(X, R)) (g : C(Y, R)) :
    prodMk' Œº ŒΩ ((f.comp .fst) * (g.comp .snd)) = Œº f * ŒΩ g := by
  simp only [prodMk'_apply, mul_comm (Œº f) (ŒΩ g), ‚Üê smul_eq_mul, ‚Üê map_smul]
  congr 1 with x
  simp_rw [ContinuousMap.smul_apply, smul_eq_mul, mul_comm (ŒΩ g) (f x), contractSnd_apply,
    ‚Üê smul_eq_mul, ‚Üê map_smul]
  rfl

end LocallyCompact

section Profinite

variable [CompactSpace X] [CompactSpace Y] [T2Space X] [T2Space Y] [TotallyDisconnectedSpace X]

/-- For profinite spaces, the two product structures on distributions agree. -/
lemma prodMk_eq_prodMk' : prodMk Œº ŒΩ = prodMk' Œº ŒΩ := by
  ext f
  rw [‚Üê sub_eq_zero, ‚Üê norm_le_zero_iff]
  refine le_of_forall_lt' (fun Œµ hŒµ ‚Ü¶ ?_)
  have hŒµ2 : 0 < Œµ / 2 := div_pos hŒµ zero_lt_two
  have := (Metric.continuousAt_iff.mp <| (Œº.prodMk ŒΩ).continuous.continuousAt (x := f)) _ hŒµ2
  rcases this with ‚ü®Œ¥, hŒ¥, hhŒ¥‚ü©
  have := (Metric.continuousAt_iff.mp <| (Œº.prodMk' ŒΩ).continuous.continuousAt (x := f)) _ hŒµ2
  rcases this with ‚ü®Œ¥', hŒ¥', hhŒ¥'‚ü©
  simp only [dist_eq_norm_sub'] at hhŒ¥ hhŒ¥'
  obtain ‚ü®n, G, H, hh‚ü© := exists_sum_mul_approx f (Metric.dist_mem_uniformity <| lt_min hŒ¥ hŒ¥')
  let T : C(X √ó Y, R) := ‚àë i, (G i).comp .fst * (H i).comp snd
  replace hh : ‚Äñf - T‚Äñ < Œ¥ ‚äì Œ¥' := by
    simpa [ContinuousMap.norm_lt_iff _ <| lt_min hŒ¥ hŒ¥', T, dist_eq_norm_sub] using hh
  calc
  _ = ‚Äñ(prodMk Œº ŒΩ f - prodMk Œº ŒΩ T) + (prodMk Œº ŒΩ T - prodMk' Œº ŒΩ f)‚Äñ := by abel_nf
  _ ‚â§ ‚ÄñprodMk Œº ŒΩ f - prodMk Œº ŒΩ T‚Äñ + ‚ÄñprodMk Œº ŒΩ T - prodMk' Œº ŒΩ f‚Äñ := norm_add_le _ _
  _ = ‚ÄñprodMk Œº ŒΩ f - prodMk Œº ŒΩ T‚Äñ + ‚ÄñprodMk' Œº ŒΩ f - prodMk Œº ŒΩ T‚Äñ := by
    congr 1; rw [norm_sub_rev]
  _ = ‚ÄñprodMk Œº ŒΩ f - prodMk Œº ŒΩ T‚Äñ + ‚ÄñprodMk' Œº ŒΩ f - prodMk' Œº ŒΩ T‚Äñ := by
    congr 3
    simp only [T, map_sum]
    congr 1 with i
    rw [prodMk_prod_apply, prodMk'_prod_apply]
  _ < Œµ := by linarith [hhŒ¥' <| hh.trans_le <| min_le_right Œ¥ Œ¥',
                hhŒ¥ <| hh.trans_le <| min_le_left Œ¥ Œ¥']

end Profinite

end Prod

section Topology

open Topology

section Weak

/--
The weak topology on `AbstractMeasure G R E` (the weakest topology such that `Œº ‚Ü¶ Œº f` is
continuous for all `f`).
-/
def WeakTopology : TopologicalSpace (AbstractMeasure X R E) :=
  .induced (fun Œº f ‚Ü¶ Œº f) inferInstance

scoped [AbstractMeasure.WeakTopology] attribute [instance] WeakTopology

end Weak

variable [CompactSpace X]

variable -- redeclare instances depending on `R`, because it needs to be *nontrivially* normed now
  {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]

noncomputable instance : Norm (AbstractMeasure X ùïú E) :=
  inferInstanceAs (Norm (C(X, ùïú) ‚ÜíL[ùïú] E))

/-- The strong topology on `AbstractMeasure G ùïú E` (the topology induced by the norm). -/
def StrongTopology : TopologicalSpace (AbstractMeasure X ùïú E) :=
  inferInstanceAs (TopologicalSpace (C(X, ùïú) ‚ÜíL[ùïú] E))

end Topology

end AbstractMeasure
