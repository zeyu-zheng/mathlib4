/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Oplax

#align_import category_theory.bicategory.functor from "leanprover-community/mathlib"@"369525b73f229ccd76a6ec0e0e0bf2be57599768"

/-!
# Pseudofunctors

A pseudofunctor is an oplax functor whose `mapId` and `mapComp` are isomorphisms. We provide
several constructors for pseudofunctors:
* `Pseudofunctor.mk` : the default constructor, which requires `mapâ‚‚_whiskerLeft` and
  `mapâ‚‚_whiskerRight` instead of naturality of `mapComp`.
* `Pseudofunctor.mkOfOplax` : construct a pseudofunctor from an oplax functor whose
  `mapId` and `mapComp` are isomorphisms. This constructor uses `Iso` to describe isomorphisms.
* `pseudofunctor.mkOfOplax'` : similar to `mkOfOplax`, but uses `IsIso` to describe
  isomorphisms.

The additional constructors are useful when constructing a pseudofunctor where the construction
of the oplax functor associated with it is already done. For example, the composition of
pseudofunctors can be defined by using the composition of oplax functors as follows:
```lean
def comp (F : Pseudofunctor B C) (G : Pseudofunctor C D) : Pseudofunctor B D :=
  mkOfOplax ((F : OplaxFunctor B C).comp G)
  { mapIdIso := fun a â†¦ (G.mapFunctor _ _).mapIso (F.mapId a) â‰ªâ‰« G.mapId (F.obj a),
    mapCompIso := fun f g â†¦
      (G.mapFunctor _ _).mapIso (F.mapComp f g) â‰ªâ‰« G.mapComp (F.map f) (F.map g) }
```
although the composition of pseudofunctors in this file is defined by using the default constructor
because `obviously` wasn't smart enough in mathlib3 and the porter of this file was too lazy
to investigate this issue further in mathlib4. Similarly, the composition is also defined by using
`mkOfOplax'` after giving appropriate instances for `IsIso`. The former constructor
`mkOfOplax` requires isomorphisms as data type `Iso`, and so it is useful if you don't want
to forget the definitions of the inverses. On the other hand, the latter constructor
`mkOfOplax'` is useful if you want to use propositional type class `IsIso`.

## Main definitions
* `CategoryTheory.Pseudofunctor B C` : a pseudofunctor between bicategories `B` and `C`
* `CategoryTheory.Pseudofunctor.comp F G` : the composition of pseudofunctors

-/

namespace CategoryTheory

open Category Bicategory

open Bicategory

universe wâ‚ wâ‚‚ wâ‚ƒ vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]
variable {D : Type uâ‚ƒ} [Bicategory.{wâ‚ƒ, vâ‚ƒ} D]

-- Porting note: this auxiliary def was introduced in Lean 3 and only used once, in this file,
-- to avoid a timeout. In Lean 4 the timeout isn't present and the definition causes other
-- things to break (simp proofs) so I removed it.
-- def Pseudofunctor.Mapâ‚‚AssociatorAux (obj : B â†’ C) (map : âˆ€ {X Y : B}, (X âŸ¶ Y) â†’ (obj X âŸ¶ obj Y))
--     (mapâ‚‚ : âˆ€ {a b : B} {f g : a âŸ¶ b}, (f âŸ¶ g) â†’ (map f âŸ¶ map g))
--    (map_comp : âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c), map (f â‰« g) â‰… map f â‰« map g) {a b c d : B}
--     (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) : Prop :=
--   mapâ‚‚ (Î±_ f g h).hom =
--     (map_comp (f â‰« g) h).hom â‰«
--       (map_comp f g).hom â–· map h â‰«
--        (Î±_ (map f) (map g) (map h)).hom â‰« map f â— (map_comp g h).inv â‰« (map_comp f (g â‰« h)).inv
#noalign category_theory.pseudofunctor.mapâ‚‚_associator_aux

/-- A pseudofunctor `F` between bicategories `B` and `C` consists of a function between objects
`F.obj`, a function between 1-morphisms `F.map`, and a function between 2-morphisms `F.mapâ‚‚`.

Unlike functors between categories, `F.map` do not need to strictly commute with the compositions,
and do not need to strictly preserve the identity. Instead, there are specified 2-isomorphisms
`F.map (ğŸ™ a) â‰… ğŸ™ (F.obj a)` and `F.map (f â‰« g) â‰… F.map f â‰« F.map g`.

`F.mapâ‚‚` strictly commute with compositions and preserve the identity. They also preserve the
associator, the left unitor, and the right unitor modulo some adjustments of domains and codomains
of 2-morphisms.
-/
structure Pseudofunctor (B : Type uâ‚) [Bicategory.{wâ‚, vâ‚} B] (C : Type uâ‚‚)
    [Bicategory.{wâ‚‚, vâ‚‚} C] extends PrelaxFunctor B C where
  mapId (a : B) : map (ğŸ™ a) â‰… ğŸ™ (obj a)
  mapComp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) : map (f â‰« g) â‰… map f â‰« map g
  mapâ‚‚_id : âˆ€ {a b : B} (f : a âŸ¶ b), mapâ‚‚ (ğŸ™ f) = ğŸ™ (map f) := by aesop_cat
  mapâ‚‚_comp :
    âˆ€ {a b : B} {f g h : a âŸ¶ b} (Î· : f âŸ¶ g) (Î¸ : g âŸ¶ h), mapâ‚‚ (Î· â‰« Î¸) = mapâ‚‚ Î· â‰« mapâ‚‚ Î¸ := by
    aesop_cat
  mapâ‚‚_whisker_left :
    âˆ€ {a b c : B} (f : a âŸ¶ b) {g h : b âŸ¶ c} (Î· : g âŸ¶ h),
      mapâ‚‚ (f â— Î·) = (mapComp f g).hom â‰« map f â— mapâ‚‚ Î· â‰« (mapComp f h).inv := by
    aesop_cat
  mapâ‚‚_whisker_right :
    âˆ€ {a b c : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g) (h : b âŸ¶ c),
      mapâ‚‚ (Î· â–· h) = (mapComp f h).hom â‰« mapâ‚‚ Î· â–· map h â‰« (mapComp g h).inv := by
    aesop_cat
  mapâ‚‚_associator :
    âˆ€ {a b c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d),
      mapâ‚‚ (Î±_ f g h).hom = (mapComp (f â‰« g) h).hom â‰« (mapComp f g).hom â–· map h â‰«
      (Î±_ (map f) (map g) (map h)).hom â‰« map f â— (mapComp g h).inv â‰«
      (mapComp f (g â‰« h)).inv := by
    aesop_cat
  mapâ‚‚_left_unitor :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      mapâ‚‚ (Î»_ f).hom = (mapComp (ğŸ™ a) f).hom â‰« (mapId a).hom â–· map f â‰« (Î»_ (map f)).hom := by
    aesop_cat
  mapâ‚‚_right_unitor :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      mapâ‚‚ (Ï_ f).hom = (mapComp f (ğŸ™ b)).hom â‰« map f â— (mapId b).hom â‰« (Ï_ (map f)).hom := by
    aesop_cat
#align category_theory.pseudofunctor CategoryTheory.Pseudofunctor

initialize_simps_projections Pseudofunctor (+toPrelaxFunctor, -obj, -map, -mapâ‚‚)

namespace Pseudofunctor

attribute [reassoc]
  mapâ‚‚_comp mapâ‚‚_whisker_left mapâ‚‚_whisker_right mapâ‚‚_associator mapâ‚‚_left_unitor mapâ‚‚_right_unitor

attribute [simp]
  mapâ‚‚_id mapâ‚‚_comp mapâ‚‚_whisker_left mapâ‚‚_whisker_right mapâ‚‚_associator mapâ‚‚_left_unitor
  mapâ‚‚_right_unitor

section

open Iso

/-- The prelax functor between the underlying quivers. -/
add_decl_doc Pseudofunctor.toPrelaxFunctor


attribute [nolint docBlame] CategoryTheory.Pseudofunctor.mapId
  CategoryTheory.Pseudofunctor.mapComp
  CategoryTheory.Pseudofunctor.mapâ‚‚_id
  CategoryTheory.Pseudofunctor.mapâ‚‚_comp
  CategoryTheory.Pseudofunctor.mapâ‚‚_whisker_left
  CategoryTheory.Pseudofunctor.mapâ‚‚_whisker_right
  CategoryTheory.Pseudofunctor.mapâ‚‚_associator
  CategoryTheory.Pseudofunctor.mapâ‚‚_left_unitor
  CategoryTheory.Pseudofunctor.mapâ‚‚_right_unitor

instance hasCoeToPrelaxFunctor : Coe (Pseudofunctor B C) (PrelaxFunctor B C) :=
  âŸ¨toPrelaxFunctorâŸ©
#align category_theory.pseudofunctor.has_coe_to_prelax_functor CategoryTheory.Pseudofunctor.hasCoeToPrelaxFunctor

variable (F : Pseudofunctor B C)

-- Porting note: `toPrelaxFunctor_eq_coe` and `to_prelaxFunctor_obj`
-- are syntactic tautologies in lean 4
#noalign category_theory.pseudofunctor.to_prelax_functor_eq_coe
#noalign category_theory.pseudofunctor.to_prelax_functor_obj

-- Porting note: removed lemma `to_prelaxFunctor_map` relating the now
-- nonexistent `PrelaxFunctor.map` and the now nonexistent `Pseudofunctor.map`
#noalign category_theory.pseudofunctor.to_prelax_functor_map

-- Porting note: removed lemma `to_prelaxFunctor_mapâ‚‚` relating
-- `PrelaxFunctor.mapâ‚‚` to nonexistent `Pseudofunctor.mapâ‚‚`
#noalign category_theory.pseudofunctor.to_prelax_functor_mapâ‚‚

/-- The oplax functor associated with a pseudofunctor. -/
def toOplax : OplaxFunctor B C :=
  { (F : PrelaxFunctor B C) with
    mapId := fun a => (F.mapId a).hom
    mapComp := fun f g => (F.mapComp f g).hom }
#align category_theory.pseudofunctor.to_oplax CategoryTheory.Pseudofunctor.toOplax

instance hasCoeToOplax : Coe (Pseudofunctor B C) (OplaxFunctor B C) :=
  âŸ¨toOplaxâŸ©
#align category_theory.pseudofunctor.has_coe_to_oplax CategoryTheory.Pseudofunctor.hasCoeToOplax

-- Porting note: `toOplax_eq_coe` is a syntactic tautology in lean 4
#noalign category_theory.pseudofunctor.to_oplax_eq_coe

@[simp]
theorem to_oplax_obj : (F : OplaxFunctor B C).obj = F.obj :=
  rfl
#align category_theory.pseudofunctor.to_oplax_obj CategoryTheory.Pseudofunctor.to_oplax_obj

-- Porting note: to_oplax_map related `OplaxFunctor.map` to `Pseudofunctor.map` but neither
-- of these exist
#noalign category_theory.pseudofunctor.to_oplax_map

-- Porting note: to_oplax_mapâ‚‚ related `OplaxFunctor.mapâ‚‚` to `Pseudofunctor.mapâ‚‚` but neither
-- of these exist
#noalign category_theory.pseudofunctor.to_oplax_mapâ‚‚

@[simp]
theorem to_oplax_mapId (a : B) : (F : OplaxFunctor B C).mapId a = (F.mapId a).hom :=
  rfl
#align category_theory.pseudofunctor.to_oplax_map_id CategoryTheory.Pseudofunctor.to_oplax_mapId

@[simp]
theorem to_oplax_mapComp {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c) :
    (F : OplaxFunctor B C).mapComp f g = (F.mapComp f g).hom :=
  rfl
#align category_theory.pseudofunctor.to_oplax_map_comp CategoryTheory.Pseudofunctor.to_oplax_mapComp

-- Porting note: I changed `simps` to `simps!` without understanding what I was doing
-- (lean 4 told me to do this)
/-- Function on 1-morphisms as a functor. -/
@[simps!]
def mapFunctor (a b : B) : (a âŸ¶ b) â¥¤ (F.obj a âŸ¶ F.obj b) :=
  (F : OplaxFunctor B C).mapFunctor a b
#align category_theory.pseudofunctor.map_functor CategoryTheory.Pseudofunctor.mapFunctor

/-- The identity pseudofunctor. -/
@[simps]
def id (B : Type uâ‚) [Bicategory.{wâ‚, vâ‚} B] : Pseudofunctor B B :=
  { PrelaxFunctor.id B with
    mapId := fun a => Iso.refl (ğŸ™ a)
    mapComp := fun f g => Iso.refl (f â‰« g) }
#align category_theory.pseudofunctor.id CategoryTheory.Pseudofunctor.id

instance : Inhabited (Pseudofunctor B B) :=
  âŸ¨id BâŸ©

/-- Composition of pseudofunctors. -/
def comp (F : Pseudofunctor B C) (G : Pseudofunctor C D) : Pseudofunctor B D :=
  { (F : PrelaxFunctor B C).comp
      (G : PrelaxFunctor C D) with
    mapId := fun a => (G.mapFunctor _ _).mapIso (F.mapId a) â‰ªâ‰« G.mapId (F.obj a)
    mapComp := fun f g =>
      (G.mapFunctor _ _).mapIso (F.mapComp f g) â‰ªâ‰« G.mapComp (F.map f) (F.map g) }
#align category_theory.pseudofunctor.comp CategoryTheory.Pseudofunctor.comp

-- `comp` is near the `maxHeartbeats` limit (and seems to go over in CI),
-- so we defer creating its `@[simp]` lemmas until a separate command.
attribute [simps] comp

@[simps]
def mapâ‚‚Iso (F : Pseudofunctor B C) {a b : B} {f g : a âŸ¶ b} (Î· : f â‰… g) : F.map f â‰… F.map g where
  hom := F.mapâ‚‚ Î·.hom
  inv := F.mapâ‚‚ Î·.inv
  hom_inv_id := by rw [â†F.mapâ‚‚_comp]; simp only [Iso.hom_inv_id, mapâ‚‚_id]
  inv_hom_id := by rw [â†F.mapâ‚‚_comp]; simp only [Iso.inv_hom_id, mapâ‚‚_id]

@[simp]
lemma to_oplax_mapâ‚‚Iso (F : Pseudofunctor B C) {a b : B} {f g : a âŸ¶ b} (Î· : f â‰… g) :
    (F : OplaxFunctor B C).mapâ‚‚Iso Î· = F.mapâ‚‚Iso Î· :=
  rfl

/-- Construct a pseudofunctor from an oplax functor whose `mapId` and `mapComp` are isomorphisms.
-/
@[simps]
def mkOfOplax (F : OplaxFunctor B C) (F' : F.PseudoCore) : Pseudofunctor B C :=
  { (F : PrelaxFunctor B C) with
    mapId := F'.mapIdIso
    mapComp := F'.mapCompIso
    mapâ‚‚_whisker_left := fun f g h Î· => by
      dsimp
      rw [F'.mapCompIso_hom f g, â† F.mapComp_naturality_right_assoc, â† F'.mapCompIso_hom f h,
        hom_inv_id, comp_id]
    mapâ‚‚_whisker_right := fun Î· h => by
      dsimp
      rw [F'.mapCompIso_hom _ h, â† F.mapComp_naturality_left_assoc, â† F'.mapCompIso_hom _ h,
        hom_inv_id, comp_id]
    mapâ‚‚_associator := fun f g h => by
      dsimp
      rw [F'.mapCompIso_hom (f â‰« g) h, F'.mapCompIso_hom f g, â† F.mapâ‚‚_associator_assoc, â†
        F'.mapCompIso_hom f (g â‰« h), â† F'.mapCompIso_hom g h, whiskerLeft_hom_inv_assoc,
        hom_inv_id, comp_id] }
#align category_theory.pseudofunctor.mk_of_oplax CategoryTheory.Pseudofunctor.mkOfOplax

/-- Construct a pseudofunctor from an oplax functor whose `mapId` and `mapComp` are isomorphisms.
-/
@[simps]
noncomputable def mkOfOplax' (F : OplaxFunctor B C) [âˆ€ a, IsIso (F.mapId a)]
    [âˆ€ {a b c} (f : a âŸ¶ b) (g : b âŸ¶ c), IsIso (F.mapComp f g)] : Pseudofunctor B C :=
  { (F : PrelaxFunctor B C) with
    mapId := fun a => asIso (F.mapId a)
    mapComp := fun f g => asIso (F.mapComp f g)
    mapâ‚‚_whisker_left := fun f g h Î· => by
      dsimp
      rw [â† assoc, IsIso.eq_comp_inv, F.mapComp_naturality_right]
    mapâ‚‚_whisker_right := fun Î· h => by
      dsimp
      rw [â† assoc, IsIso.eq_comp_inv, F.mapComp_naturality_left]
    mapâ‚‚_associator := fun f g h => by
      dsimp
      simp only [â† assoc]
      rw [IsIso.eq_comp_inv, â† inv_whiskerLeft, IsIso.eq_comp_inv]
      simp only [assoc, F.mapâ‚‚_associator] }
#align category_theory.pseudofunctor.mk_of_oplax' CategoryTheory.Pseudofunctor.mkOfOplax'

end

-- API
section

variable (F : Pseudofunctor B C) {a b : B}

-- These should be for Oplax functors!!!!!!!
lemma mapComp_assoc_right_hom {c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    (F.mapComp f (g â‰« h)).hom â‰« F.map f â— (F.mapComp g h).hom = (F.mapâ‚‚ (Î±_ f g h).inv) â‰«
    (F.mapComp (f â‰« g) h).hom â‰« (F.mapComp f g).hom â–· F.map h â‰«
    (Î±_ (F.map f) (F.map g) (F.map h)).hom := by
  sorry

lemma mapComp_assoc_left_hom {c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    (F.mapComp (f â‰« g) h).hom â‰« (F.mapComp f g).hom â–· F.map h =
    (F.mapâ‚‚ (Î±_ f g h).hom) â‰« (F.mapComp f (g â‰« h)).hom â‰« F.map f â— (F.mapComp g h).hom
    â‰« (Î±_ (F.map f) (F.map g) (F.map h)).inv := by
  simp

-- TODO: this should follow from a statement about lax functors, once implemented
lemma mapComp_assoc_right_inv {c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    F.map f â— (F.mapComp g h).inv â‰« (F.mapComp f (g â‰« h)).inv =
    (Î±_ (F.map f) (F.map g) (F.map h)).inv â‰« (F.mapComp f g).inv â–· F.map h â‰«
    (F.mapComp (f â‰« g) h).inv â‰« (F.mapâ‚‚ (Î±_ f g h).hom) := by
  simp

-- TODO: this should follow from a statement about lax functors, once implemented
lemma mapComp_assoc_left_inv {c d : B} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    (F.mapComp f g).inv â–· F.map h â‰« (F.mapComp (f â‰« g) h).inv =
    (Î±_ (F.map f) (F.map g) (F.map h)).hom â‰« F.map f â— (F.mapComp g h).inv â‰«
    (F.mapComp f (g â‰« h)).inv â‰« (F.mapâ‚‚ (Î±_ f g h).inv) := by
  sorry

lemma mapComp_id_left (f : a âŸ¶ b) : (F.mapComp (ğŸ™ a) f) = F.mapâ‚‚Iso (Î»_ f) â‰ªâ‰«
    (Î»_ (F.map f)).symm â‰ªâ‰« (whiskerRightIso (F.mapId a) (F.map f)).symm := by
  ext; simp

lemma mapComp_id_left_hom (f : a âŸ¶ b) : (F.mapComp (ğŸ™ a) f).hom =
    F.mapâ‚‚ (Î»_ f).hom â‰« (Î»_ (F.map f)).inv â‰« (F.mapId a).inv â–· (F.map f) := by
  simp

lemma mapComp_id_left_inv (f : a âŸ¶ b) : (F.mapComp (ğŸ™ a) f).inv =
    (F.mapId a).hom â–· (F.map f) â‰« (Î»_ (F.map f)).hom â‰« F.mapâ‚‚ (Î»_ f).inv := by
  simp [mapComp_id_left]

lemma mapId_whiskerRightIso (f : a âŸ¶ b) : whiskerRightIso (F.mapId a) (F.map f) =
    (F.mapComp (ğŸ™ a) f).symm â‰ªâ‰« F.mapâ‚‚Iso (Î»_ f) â‰ªâ‰« (Î»_ (F.map f)).symm := by
  simp [mapComp_id_left]

lemma mapId_whiskerRight_hom (f : a âŸ¶ b) : (F.mapId a).hom â–· F.map f =
    (F.mapComp (ğŸ™ a) f).inv â‰« F.mapâ‚‚ (Î»_ f).hom â‰« (Î»_ (F.map f)).inv := by
  simp [mapId_whiskerRightIso]

lemma mapId_whiskerRight_inv (f : a âŸ¶ b) : (F.mapId a).inv â–· F.map f =
    (Î»_ (F.map f)).hom â‰« F.mapâ‚‚ (Î»_ f).inv â‰« (F.mapComp (ğŸ™ a) f).hom := by
  simpa using congrArg (Â·.inv) (F.mapId_whiskerRightIso f)

lemma mapComp_id_right (f : a âŸ¶ b) : (F.mapComp f (ğŸ™ b)) = F.mapâ‚‚Iso (Ï_ f) â‰ªâ‰«
    (Ï_ (F.map f)).symm â‰ªâ‰« (whiskerLeftIso (F.map f) (F.mapId b)).symm := by
  ext; simp

lemma mapComp_id_right_hom (f : a âŸ¶ b) : (F.mapComp f (ğŸ™ b)).hom =
    F.mapâ‚‚ (Ï_ f).hom â‰« (Ï_ (F.map f)).inv â‰« F.map f â— (F.mapId b).inv := by
  simp

lemma mapComp_id_right_inv (f : a âŸ¶ b) : (F.mapComp f (ğŸ™ b)).inv =
    (F.map f) â— (F.mapId b).hom â‰« (Ï_ (F.map f)).hom â‰« F.mapâ‚‚ (Ï_ f).inv := by
  simp [mapComp_id_right]

lemma mapId_whiskerLeftIso (f : a âŸ¶ b) : whiskerLeftIso (F.map f) (F.mapId b) =
    (F.mapComp f (ğŸ™ b)).symm â‰ªâ‰« F.mapâ‚‚Iso (Ï_ f) â‰ªâ‰« (Ï_ (F.map f)).symm := by
  ext; simp

lemma mapId_whiskerLeft_hom (f : a âŸ¶ b) : F.map f â— (F.mapId b).hom =
    (F.mapComp f (ğŸ™ b)).inv â‰« F.mapâ‚‚ (Ï_ f).hom â‰« (Ï_ (F.map f)).inv := by
  simp [mapId_whiskerLeftIso]

lemma mapId_whiskerLeft_inv (f : a âŸ¶ b) : F.map f â— (F.mapId b).inv =
    (Ï_ (F.map f)).hom â‰« F.mapâ‚‚ (Ï_ f).inv â‰« (F.mapComp f (ğŸ™ b)).hom := by
  simpa using congrArg (Â·.inv) (F.mapId_whiskerLeftIso f)


end

end Pseudofunctor

end CategoryTheory
