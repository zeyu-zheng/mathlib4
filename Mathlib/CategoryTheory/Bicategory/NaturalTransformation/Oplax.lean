/-
Copyright (c) 2022 Yuma Mizuno. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yuma Mizuno
-/
import Mathlib.CategoryTheory.Bicategory.Functor.Oplax

/-!
# Natural transformations between oplax functors

In this file we define different types of natural transformations between oplax functors. There are
three types of these. The basic idea is that instead of satisfying a naturality condition up to
equality, as for ordinary natural transformations, they are only satisfied up to some specified
2-morphism. This 2-morphism depends on the type of the natural transformation. There are three
types:
* Oplax natural transformations. Then the specified 2-morphism is
`F.map f â‰« app b âŸ¶ app a â‰« G.map f`.
* Lax natural transformations. The specified 2-morphism is reversed in this case, i.e.
`app b â‰« G.map f âŸ¶ F.map f â‰« app a`.
* Strong natural transformations. In this case the specified 2-morphism is an isomorphism.

## Main definitions

* `OplaxNatTrans F G` : oplax natural transformations between oplax functors `F` and `G`
* `OplaxNatTrans.vcomp Î· Î¸` : the vertical composition of oplax natural transformations `Î·`
  and `Î¸`
* `OplaxNatTrans.category F G` : the category structure on the oplax natural transformations
  between `F` and `G`
-/

namespace CategoryTheory

open Category Bicategory

open scoped Bicategory

universe wâ‚ wâ‚‚ vâ‚ vâ‚‚ uâ‚ uâ‚‚

namespace OplaxFunctor

variable {B : Type uâ‚} [Bicategory.{wâ‚, vâ‚} B] {C : Type uâ‚‚} [Bicategory.{wâ‚‚, vâ‚‚} C]

/-- If `Î·` is an oplax natural transformation between `F` and `G`, we have a 1-morphism
`Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`. We also have a 2-morphism
`Î·.naturality f : F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism `f : a âŸ¶ b`.
These 2-morphisms satisfies the naturality condition, and preserve the identities and
the compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure OplaxNatTrans (F G : OplaxFunctor B C) where
  /-- The component 1-morphism. -/
  app (a : B) : F.obj a âŸ¶ G.obj a
  /-- The 2-morphism underlying the oplax naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« app b âŸ¶ app a â‰« G.map f
  /-- Naturality of the oplax naturality constraint. -/
  naturality_naturality :
    âˆ€ {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
      F.mapâ‚‚ Î· â–· app b â‰« naturality g = naturality f â‰« app a â— G.mapâ‚‚ Î· := by
    aesop_cat
  /-- The oplax naturality constraint is compatible with the oplax unity constraint. -/
  naturality_id :
    âˆ€ a : B,
      naturality (ğŸ™ a) â‰« app a â— G.mapId a =
        F.mapId a â–· app a â‰« (Î»_ (app a)).hom â‰« (Ï_ (app a)).inv := by
    aesop_cat
  /-- The oplax naturality constraint is compatible with the oplax functoriality constraint. -/
  naturality_comp :
    âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
      naturality (f â‰« g) â‰« app a â— G.mapComp f g =
        F.mapComp f g â–· app c â‰«
          (Î±_ _ _ _).hom â‰«
            F.map f â— naturality g â‰« (Î±_ _ _ _).inv â‰« naturality f â–· G.map g â‰« (Î±_ _ _ _).hom := by
    aesop_cat

attribute [reassoc (attr := simp)] OplaxNatTrans.naturality_naturality OplaxNatTrans.naturality_id
  OplaxNatTrans.naturality_comp

namespace OplaxNatTrans

section

variable (F : OplaxFunctor B C)

/-- The identity oplax natural transformation. -/
@[simps]
def id : OplaxNatTrans F F where
  app a := ğŸ™ (F.obj a)
  naturality {a b} f := (Ï_ (F.map f)).hom â‰« (Î»_ (F.map f)).inv

instance : Inhabited (OplaxNatTrans F F) :=
  âŸ¨id FâŸ©

variable {F} {G H : OplaxFunctor B C} (Î· : OplaxNatTrans F G) (Î¸ : OplaxNatTrans G H)

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_naturality (f : a' âŸ¶ G.obj a) {g h : a âŸ¶ b} (Î² : g âŸ¶ h) :
    f â— G.mapâ‚‚ Î² â–· Î¸.app b â‰« f â— Î¸.naturality h =
      f â— Î¸.naturality g â‰« f â— Î¸.app a â— H.mapâ‚‚ Î² := by
  rw [â† whiskerLeft_comp, â† whiskerLeft_comp, naturality_naturality]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_naturality {f g : a âŸ¶ b} (Î² : f âŸ¶ g) (h : G.obj b âŸ¶ a') :
    F.mapâ‚‚ Î² â–· Î·.app b â–· h â‰« Î·.naturality g â–· h =
      Î·.naturality f â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapâ‚‚ Î² â–· h â‰« (Î±_ _ _ _).inv := by
  rw [â† comp_whiskerRight, naturality_naturality, comp_whiskerRight, whisker_assoc]

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_comp (f : a' âŸ¶ G.obj a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
    f â— Î¸.naturality (g â‰« h) â‰« f â— Î¸.app a â— H.mapComp g h =
      f â— G.mapComp g h â–· Î¸.app c â‰«
        f â— (Î±_ _ _ _).hom â‰«
          f â— G.map g â— Î¸.naturality h â‰«
            f â— (Î±_ _ _ _).inv â‰« f â— Î¸.naturality g â–· H.map h â‰« f â— (Î±_ _ _ _).hom := by
  simp_rw [â† whiskerLeft_comp, naturality_comp]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.obj c âŸ¶ a') :
    Î·.naturality (f â‰« g) â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapComp f g â–· h =
      F.mapComp f g â–· Î·.app c â–· h â‰«
        (Î±_ _ _ _).hom â–· h â‰«
          (Î±_ _ _ _).hom â‰«
            F.map f â— Î·.naturality g â–· h â‰«
              (Î±_ _ _ _).inv â‰«
                (Î±_ _ _ _).inv â–· h â‰«
                  Î·.naturality f â–· G.map g â–· h â‰« (Î±_ _ _ _).hom â–· h â‰« (Î±_ _ _ _).hom := by
  rw [â† associator_naturality_middle, â† comp_whiskerRight_assoc, naturality_comp]; simp

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_id (f : a' âŸ¶ G.obj a) :
    f â— Î¸.naturality (ğŸ™ a) â‰« f â— Î¸.app a â— H.mapId a =
      f â— G.mapId a â–· Î¸.app a â‰« f â— (Î»_ (Î¸.app a)).hom â‰« f â— (Ï_ (Î¸.app a)).inv := by
  simp_rw [â† whiskerLeft_comp, naturality_id]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_id (f : G.obj a âŸ¶ a') :
    Î·.naturality (ğŸ™ a) â–· f â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapId a â–· f =
    F.mapId a â–· Î·.app a â–· f â‰« (Î»_ (Î·.app a)).hom â–· f â‰« (Ï_ (Î·.app a)).inv â–· f â‰« (Î±_ _ _ _).hom := by
  rw [â† associator_naturality_middle, â† comp_whiskerRight_assoc, naturality_id]; simp

end

/-- Vertical composition of oplax natural transformations. -/
@[simps]
def vcomp (Î· : OplaxNatTrans F G) (Î¸ : OplaxNatTrans G H) : OplaxNatTrans F H where
  app a := Î·.app a â‰« Î¸.app a
  naturality {a b} f :=
    (Î±_ _ _ _).inv â‰«
      Î·.naturality f â–· Î¸.app b â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— Î¸.naturality f â‰« (Î±_ _ _ _).inv
  naturality_comp {a b c} f g := by
    calc
      _ =
          ?_ â‰«
            F.mapComp f g â–· Î·.app c â–· Î¸.app c â‰«
              ?_ â‰«
                F.map f â— Î·.naturality g â–· Î¸.app c â‰«
                  ?_ â‰«
                    (F.map f â‰« Î·.app b) â— Î¸.naturality g â‰«
                      Î·.naturality f â–· (Î¸.app b â‰« H.map g) â‰«
                        ?_ â‰« Î·.app a â— Î¸.naturality f â–· H.map g â‰« ?_ :=
        ?_
      _ = _ := ?_
    Â· exact (Î±_ _ _ _).inv
    Â· exact (Î±_ _ _ _).hom â–· _ â‰« (Î±_ _ _ _).hom
    Â· exact _ â— (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv
    Â· exact (Î±_ _ _ _).hom â‰« _ â— (Î±_ _ _ _).inv
    Â· exact _ â— (Î±_ _ _ _).hom â‰« (Î±_ _ _ _).inv
    Â· rw [whisker_exchange_assoc]
      simp
    Â· simp

variable (B C)

@[simps id comp]
instance : CategoryStruct (OplaxFunctor B C) where
  Hom := OplaxNatTrans
  id := OplaxNatTrans.id
  comp := OplaxNatTrans.vcomp

end

section

variable {F G : OplaxFunctor B C}

/-- A modification `Î“` between oplax natural transformations `Î·` and `Î¸` consists of a family of
2-morphisms `Î“.app a : Î·.app a âŸ¶ Î¸.app a`, which satisfies the equation
`(F.map f â— app b) â‰« Î¸.naturality f = Î·.naturality f â‰« (app a â–· G.map f)`
for each 1-morphism `f : a âŸ¶ b`.
-/
@[ext]
structure Modification (Î· Î¸ : F âŸ¶ G) where
  app (a : B) : Î·.app a âŸ¶ Î¸.app a
  naturality :
    âˆ€ {a b : B} (f : a âŸ¶ b),
      F.map f â— app b â‰« Î¸.naturality f = Î·.naturality f â‰« app a â–· G.map f := by
    aesop_cat

attribute [nolint docBlame] CategoryTheory.OplaxFunctor.OplaxNatTrans.Modification.app
  CategoryTheory.OplaxFunctor.OplaxNatTrans.Modification.naturality

/- Porting note: removed primes from field names and removed `restate_axiom` since that is no longer
  needed in Lean 4 -/

attribute [reassoc (attr := simp)] Modification.naturality

variable {Î· Î¸ Î¹ : F âŸ¶ G}

namespace Modification

variable (Î·)

/-- The identity modification. -/
@[simps]
def id : Modification Î· Î· where app a := ğŸ™ (Î·.app a)

instance : Inhabited (Modification Î· Î·) :=
  âŸ¨Modification.id Î·âŸ©

variable {Î·}

section

variable (Î“ : Modification Î· Î¸) {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality (f : a' âŸ¶ F.obj b) (g : b âŸ¶ c) :
    f â— F.map g â— Î“.app c â‰« f â— Î¸.naturality g = f â— Î·.naturality g â‰« f â— Î“.app b â–· G.map g := by
  simp_rw [â† whiskerLeft_comp, naturality]

@[reassoc (attr := simp)]
theorem whiskerRight_naturality (f : a âŸ¶ b) (g : G.obj b âŸ¶ a') :
    F.map f â— Î“.app b â–· g â‰« (Î±_ _ _ _).inv â‰« Î¸.naturality f â–· g =
      (Î±_ _ _ _).inv â‰« Î·.naturality f â–· g â‰« Î“.app a â–· G.map f â–· g := by
  simp_rw [associator_inv_naturality_middle_assoc, â† comp_whiskerRight, naturality]

end

/-- Vertical composition of modifications. -/
@[simps]
def vcomp (Î“ : Modification Î· Î¸) (Î” : Modification Î¸ Î¹) : Modification Î· Î¹ where
  app a := Î“.app a â‰« Î”.app a

end Modification

/-- Category structure on the oplax natural transformations between OplaxFunctors. -/
@[simps]
instance category (F G : OplaxFunctor B C) : Category (F âŸ¶ G) where
  Hom := Modification
  id := Modification.id
  comp := Modification.vcomp

-- Porting note: duplicating the `ext` lemma.
@[ext]
lemma ext {F G : OplaxFunctor B C} {Î± Î² : F âŸ¶ G} {m n : Î± âŸ¶ Î²} (w : âˆ€ b, m.app b = n.app b) :
    m = n := by
  apply Modification.ext
  ext
  apply w

@[simp]
lemma Modification.id_app' {X : B} {F G : OplaxFunctor B C} (Î± : F âŸ¶ G) :
    Modification.app (ğŸ™ Î±) X = ğŸ™ (Î±.app X) := rfl

@[simp]
lemma Modification.comp_app' {X : B} {F G : OplaxFunctor B C} {Î± Î² Î³ : F âŸ¶ G}
    (m : Î± âŸ¶ Î²) (n : Î² âŸ¶ Î³) : (m â‰« n).app X = m.app X â‰« n.app X :=
  rfl

/-- Construct a modification isomorphism between oplax natural transformations
by giving object level isomorphisms, and checking naturality only in the forward direction.
-/
@[simps]
def ModificationIso.ofComponents (app : âˆ€ a, Î·.app a â‰… Î¸.app a)
    (naturality :
      âˆ€ {a b} (f : a âŸ¶ b),
        F.map f â— (app b).hom â‰« Î¸.naturality f = Î·.naturality f â‰« (app a).hom â–· G.map f) :
    Î· â‰… Î¸ where
  hom := { app := fun a => (app a).hom }
  inv :=
    { app := fun a => (app a).inv
      naturality := fun {a b} f => by
        simpa using congr_arg (fun f => _ â— (app b).inv â‰« f â‰« (app a).inv â–· _) (naturality f).symm }

end

/-- A structure on an Oplax natural transformation that promotes it to a strong natural
transformation.

See `StrongNatTrans.mkOfOplax`. -/
structure StrongCore {F G : OplaxFunctor B C} (Î· : OplaxNatTrans F G) where
  naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« Î·.app b â‰… Î·.app a â‰« G.map f
  naturality_hom {a b : B} (f : a âŸ¶ b) : (naturality f).hom = Î·.naturality f := by aesop_cat

attribute [nolint docBlame] CategoryTheory.OplaxFunctor.OplaxNatTrans.StrongCore.naturality
  CategoryTheory.OplaxFunctor.OplaxNatTrans.StrongCore.naturality_hom

attribute [simp] StrongCore.naturality_hom

end OplaxNatTrans

/-- A strong natural transformation between oplax functors `F` and `G` is a natural transformation
that is "natural up to 2-isomorphisms".

More precisely, it consists of the following:
* a 1-morphism `Î·.app a : F.obj a âŸ¶ G.obj a` for each object `a : B`.
* a 2-isomorphism `Î·.naturality f : F.map f â‰« app b âŸ¶ app a â‰« G.map f` for each 1-morphism
`f : a âŸ¶ b`.
* These 2-isomorphisms satisfy the naturality condition, and preserve the identities and the
compositions modulo some adjustments of domains and codomains of 2-morphisms.
-/
structure StrongNatTrans (F G : OplaxFunctor B C) where
  /-- The component 1-morphism. -/
  app (a : B) : F.obj a âŸ¶ G.obj a
  /-- The 2-isomorphism underlying the strong naturality constraint. -/
  naturality {a b : B} (f : a âŸ¶ b) : F.map f â‰« app b â‰… app a â‰« G.map f
  /-- Naturality of the strong naturality constraint. -/
  naturality_naturality :
    âˆ€ {a b : B} {f g : a âŸ¶ b} (Î· : f âŸ¶ g),
      F.mapâ‚‚ Î· â–· app b â‰« (naturality g).hom = (naturality f).hom â‰« app a â— G.mapâ‚‚ Î· := by
    aesop_cat
  /-- The strong naturality constraint is compatible with the oplax unity constraint. -/
  naturality_id :
    âˆ€ a : B,
      (naturality (ğŸ™ a)).hom â‰« app a â— G.mapId a =
        F.mapId a â–· app a â‰« (Î»_ (app a)).hom â‰« (Ï_ (app a)).inv := by
    aesop_cat
  /-- The strong naturality constraint is compatible with the oplax functoriality constraint. -/
  naturality_comp :
    âˆ€ {a b c : B} (f : a âŸ¶ b) (g : b âŸ¶ c),
      (naturality (f â‰« g)).hom â‰« app a â— G.mapComp f g =
        F.mapComp f g â–· app c â‰« (Î±_ _ _ _).hom â‰« F.map f â— (naturality g).hom â‰«
        (Î±_ _ _ _).inv â‰« (naturality f).hom â–· G.map g â‰« (Î±_ _ _ _).hom := by
    aesop_cat

attribute [reassoc (attr := simp)] StrongNatTrans.naturality_naturality
  StrongNatTrans.naturality_id StrongNatTrans.naturality_comp

namespace StrongNatTrans

section

/-- The underlying oplax natural transformation of a strong natural transformation. -/
@[simps]
def toOplax {F G : OplaxFunctor B C} (Î· : StrongNatTrans F G) : OplaxNatTrans F G where
  app := Î·.app
  naturality f := (Î·.naturality f).hom

/-- Construct a strong natural transformation from an oplax natural transformation whose
naturality 2-cell is an isomorphism. -/
def mkOfOplax {F G : OplaxFunctor B C} (Î· : OplaxNatTrans F G) (Î·' : OplaxNatTrans.StrongCore Î·) :
    StrongNatTrans F G where
  app := Î·.app
  naturality := Î·'.naturality

/-- Construct a strong natural transformation from an oplax natural transformation whose
naturality 2-cell is an isomorphism. -/
noncomputable def mkOfOplax' {F G : OplaxFunctor B C} (Î· : OplaxNatTrans F G)
    [âˆ€ a b (f : a âŸ¶ b), IsIso (Î·.naturality f)] : StrongNatTrans F G where
  app := Î·.app
  naturality := fun f => asIso (Î·.naturality _)

variable (F : OplaxFunctor B C)


/-- The identity strong natural transformation. -/
@[simps!]
def id : StrongNatTrans F F :=
  mkOfOplax (OplaxNatTrans.id F) { naturality := Î» f â†¦ (Ï_ (F.map f)) â‰ªâ‰« (Î»_ (F.map f)).symm }

@[simp]
lemma id.toOplax : (id F).toOplax = OplaxNatTrans.id F :=
  rfl

instance : Inhabited (StrongNatTrans F F) :=
  âŸ¨id FâŸ©

variable {F} {G H : OplaxFunctor B C} (Î· : StrongNatTrans F G) (Î¸ : StrongNatTrans G H)

section

variable {a b c : B} {a' : C}

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_naturality (f : a' âŸ¶ G.obj a) {g h : a âŸ¶ b} (Î² : g âŸ¶ h) :
    f â— G.mapâ‚‚ Î² â–· Î¸.app b â‰« f â— (Î¸.naturality h).hom =
      f â— (Î¸.naturality g).hom â‰« f â— Î¸.app a â— H.mapâ‚‚ Î² := by
  apply Î¸.toOplax.whiskerLeft_naturality_naturality

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_naturality {f g : a âŸ¶ b} (Î² : f âŸ¶ g) (h : G.obj b âŸ¶ a') :
    F.mapâ‚‚ Î² â–· Î·.app b â–· h â‰« (Î·.naturality g).hom â–· h =
      (Î·.naturality f).hom â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapâ‚‚ Î² â–· h â‰« (Î±_ _ _ _).inv := by
  apply Î·.toOplax.whiskerRight_naturality_naturality

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_comp (f : a' âŸ¶ G.obj a) (g : a âŸ¶ b) (h : b âŸ¶ c) :
    f â— (Î¸.naturality (g â‰« h)).hom â‰« f â— Î¸.app a â— H.mapComp g h =
      f â— G.mapComp g h â–· Î¸.app c â‰«
        f â— (Î±_ _ _ _).hom â‰«
          f â— G.map g â— (Î¸.naturality h).hom â‰«
            f â— (Î±_ _ _ _).inv â‰« f â— (Î¸.naturality g).hom â–· H.map h â‰« f â— (Î±_ _ _ _).hom := by
  apply Î¸.toOplax.whiskerLeft_naturality_comp

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_comp (f : a âŸ¶ b) (g : b âŸ¶ c) (h : G.obj c âŸ¶ a') :
    (Î·.naturality (f â‰« g)).hom â–· h â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapComp f g â–· h =
      F.mapComp f g â–· Î·.app c â–· h â‰«
        (Î±_ _ _ _).hom â–· h â‰«
          (Î±_ _ _ _).hom â‰«
            F.map f â— (Î·.naturality g).hom â–· h â‰«
              (Î±_ _ _ _).inv â‰«
                (Î±_ _ _ _).inv â–· h â‰«
                 (Î·.naturality f).hom â–· G.map g â–· h â‰« (Î±_ _ _ _).hom â–· h â‰« (Î±_ _ _ _).hom := by
  apply Î·.toOplax.whiskerRight_naturality_comp

@[reassoc (attr := simp)]
theorem whiskerLeft_naturality_id (f : a' âŸ¶ G.obj a) :
    f â— (Î¸.naturality (ğŸ™ a)).hom â‰« f â— Î¸.app a â— H.mapId a =
      f â— G.mapId a â–· Î¸.app a â‰« f â— (Î»_ (Î¸.app a)).hom â‰« f â— (Ï_ (Î¸.app a)).inv := by
  apply Î¸.toOplax.whiskerLeft_naturality_id

@[reassoc (attr := simp)]
theorem whiskerRight_naturality_id (f : G.obj a âŸ¶ a') :
    (Î·.naturality (ğŸ™ a)).hom â–· f â‰« (Î±_ _ _ _).hom â‰« Î·.app a â— G.mapId a â–· f =
    F.mapId a â–· Î·.app a â–· f â‰« (Î»_ (Î·.app a)).hom â–· f â‰« (Ï_ (Î·.app a)).inv â–· f â‰«
    (Î±_ _ _ _).hom := by
  apply Î·.toOplax.whiskerRight_naturality_id

end

/-- Vertical composition of strong natural transformations. -/
@[simps!]
def vcomp (Î· : StrongNatTrans F G) (Î¸ : StrongNatTrans G H) : StrongNatTrans F H :=
  mkOfOplax (OplaxNatTrans.vcomp Î·.toOplax Î¸.toOplax)
    { naturality := Î» {a b} f â†¦
        (Î±_ _ _ _).symm â‰ªâ‰« whiskerRightIso (Î·.naturality f) (Î¸.app b) â‰ªâ‰«
        (Î±_ _ _ _) â‰ªâ‰« whiskerLeftIso (Î·.app a) (Î¸.naturality f) â‰ªâ‰« (Î±_ _ _ _).symm }
end

end StrongNatTrans

end OplaxFunctor

end CategoryTheory
