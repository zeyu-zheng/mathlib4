/-
Copyright (c) 2024 Calle SÃ¶nne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Lezeau, Calle SÃ¶nne
-/

import Mathlib.CategoryTheory.FiberedCategory.HasFibers
import Mathlib.CategoryTheory.Bicategory.Functor.Strict
import Mathlib.CategoryTheory.Category.Cat
import Mathlib.CategoryTheory.Bicategory.LocallyDiscrete

/-!
# The fibered category associated to a pseudofunctor

Given a category `ğ’®` and any pseudofunctor valued in `Cat` we associate to it a fibered category
category `F.toFibered â¥¤ ğ’®`.

The category `F.toFibered` is defined as follows:
* Objects: pairs `(S, a)` where `S` is an object of the base category and `a` is an object of the
  category `F(S)`
* Morphisms: morphisms `(R, b) âŸ¶ (S, a)` are defined as pairs `(f, h)` where `f : R âŸ¶ S` is a
  morphism in `ğ’®` and `h : b âŸ¶ F(f)(a)`

The projection functor `F.toFibered â¥¤ ğ’®` is then given by projecting to the first factors, i.e.
* On objects, it sends `(S, a)` to `S`
* On morphisms, it sends `(f, h)` to `f`

We also provide a `HasFibers` instance `F.toFibered`, such that the fiber over `S` is the category `F(S)`.

## References
[Vistoli2008] "Notes on Grothendieck Topologies, Fibered Categories and Descent Theory" by
Angelo Vistoli

-/

/-
TODO:
- Fix "â†‘F.toPrelaxFunctor.obj" instead of "F.obj"
- Fix naming
- (Later) splittings & functoriality
- Make `presheaf.lean` a special instance of the above
  - Isomorphism between the overcategory and fibered category associated to the corresponding
  presheaf?
-/

namespace CategoryTheory

universe w vâ‚ vâ‚‚ vâ‚ƒ uâ‚ uâ‚‚ uâ‚ƒ

open CategoryTheory Functor Category Opposite Discrete Bicategory

section mathlib_lemmas

-- these will be in mathlib soon
lemma Cat.whiskerLeft_app {C D E : Cat} (F : C âŸ¶ D) {G H : D âŸ¶ E} (Î· : G âŸ¶ H) (X : C) :
    (F â— Î·).app X = Î·.app (F.obj X) :=
  CategoryTheory.whiskerLeft_app F Î· X

lemma Cat.whiskerRight_app {C D E : Cat} {F G : C âŸ¶ D} (H : D âŸ¶ E) (Î· : F âŸ¶ G) (X : C) :
    (Î· â–· H).app X = H.map (Î·.app X) :=
  CategoryTheory.whiskerRight_app Î· H X

-- already in mathlib!
@[simp]
lemma Quiver.Hom.eqToHom_toLoc {C : Type uâ‚} [Category.{vâ‚} C] {a b : C}
    (h : a = b) : (eqToHom h).toLoc = eqToHom (congrArg LocallyDiscrete.mk h) := by
  subst h; rfl

end mathlib_lemmas

variable {ğ’® : Type uâ‚} [Category.{vâ‚} ğ’®] {F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}}

/-- The type of objects in the fibered category associated to a presheaf valued in types. -/
def Pseudofunctor.toFibered (F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}) :=
  (S : ğ’®) Ã— (F.obj âŸ¨op SâŸ©)

namespace Pseudofunctor.toFibered

@[simps]
instance CategoryStruct : CategoryStruct F.toFibered where
  Hom X Y := (f : X.1 âŸ¶ Y.1) Ã— (X.2 âŸ¶ (F.map f.op.toLoc).obj Y.2)
  id X := âŸ¨ğŸ™ X.1, (F.mapId âŸ¨op X.1âŸ©).inv.app X.2âŸ©
  comp {_ _ Z} f g := âŸ¨f.1 â‰« g.1, f.2 â‰« (F.map f.1.op.toLoc).map g.2 â‰«
    (F.mapComp g.1.op.toLoc f.1.op.toLoc).inv.app Z.2âŸ©

section

variable {a b : F.toFibered} (f : a âŸ¶ b)

@[ext]
lemma hom_ext (g : a âŸ¶ b) (hfgâ‚ : f.1 = g.1) (hfgâ‚‚ : f.2 = g.2 â‰« eqToHom (hfgâ‚ â–¸ rfl)) :
    f = g := by
  apply Sigma.ext hfgâ‚
  rw [â† conj_eqToHom_iff_heq _ _ rfl (hfgâ‚ â–¸ rfl)]
  simp only [hfgâ‚‚, eqToHom_refl, id_comp]

-- Might not need this lemma in the end
lemma hom_ext_iff (g : a âŸ¶ b) : f = g â†” âˆƒ (hfg : f.1 = g.1), f.2 = g.2 â‰« eqToHom (hfg â–¸ rfl) where
  mp := fun hfg => âŸ¨by rw [hfg], by simp [hfg]âŸ©
  mpr := fun âŸ¨hfgâ‚, hfgâ‚‚âŸ© => hom_ext f g hfgâ‚ hfgâ‚‚

protected lemma id_comp : ğŸ™ a â‰« f = f := by
  ext
  Â· simp
  dsimp
  rw [F.mapComp_id_right_ofStrict_inv f.1.op.toLoc]
  rw [â† (F.mapId âŸ¨op a.1âŸ©).inv.naturality_assoc f.2]
  conv_lhs =>
    congr; rfl;
    rw [â† Cat.whiskerLeft_app, â† NatTrans.comp_app, â† assoc]
    rw [â† Bicategory.whiskerLeft_comp, Iso.inv_hom_id]
  simp

protected lemma comp_id : f â‰« ğŸ™ b = f := by
  ext
  Â· simp
  dsimp
  rw [F.mapComp_id_left_ofStrict_inv f.1.op.toLoc]
  rw [â† Cat.whiskerRight_app, â† NatTrans.comp_app]
  nth_rw 1 [â† assoc]
  rw [â† Bicategory.comp_whiskerRight, Iso.inv_hom_id]
  simp

end

protected lemma assoc {a b c d : F.toFibered} (f : a âŸ¶ b) (g : b âŸ¶ c) (h : c âŸ¶ d) :
    (f â‰« g) â‰« h = f â‰« g â‰« h := by
  ext
  Â· simp
  dsimp
  conv_lhs =>
    rw [assoc, assoc]
    rw [â† (F.mapComp g.1.op.toLoc f.1.op.toLoc).inv.naturality_assoc h.2]
    rw [â† Cat.whiskerLeft_app, â† NatTrans.comp_app]
    rw [F.mapâ‚‚_associator_ofStrict_inv h.1.op.toLoc g.1.op.toLoc f.1.op.toLoc]
    rw [NatTrans.comp_app, NatTrans.comp_app, eqToHom_app, eqToHom_app, eqToHom_refl, id_comp]
  conv_rhs => simp only [Cat.comp_obj, Cat.comp_map, map_comp, assoc]
  congr 3
  rw [â† Cat.whiskerRight_app, NatTrans.comp_app]
  simp only [Cat.comp_obj, assoc]


/-- The category structure on the fibered category associated to a presheaf valued in types. -/
instance : Category (F.toFibered) where
  toCategoryStruct := Pseudofunctor.toFibered.CategoryStruct
  id_comp := Pseudofunctor.toFibered.id_comp
  comp_id := Pseudofunctor.toFibered.comp_id
  assoc := Pseudofunctor.toFibered.assoc

/-- The projection `F.toFibered â¥¤ ğ’®` given by projecting both objects and homs to the first factor -/
@[simps]
def Ï€ (F : Pseudofunctor (LocallyDiscrete ğ’®áµ’áµ–) Cat.{vâ‚‚, uâ‚‚}) : F.toFibered â¥¤ ğ’® where
  obj := fun X => X.1
  map := fun f => f.1

section

variable {R S : ğ’®} (a : F.obj âŸ¨op SâŸ©) (f : R âŸ¶ S)

-- TODO: improve comment after I know final form of this...
/-- An object of `F.toFibered` lying over `S`, given by some `a : F(T)` and `S âŸ¶ T` -/
abbrev pullback_obj : F.toFibered := âŸ¨R, (F.map f.op.toLoc).obj aâŸ©

abbrev pullback_map : pullback_obj a f âŸ¶ âŸ¨S, aâŸ© := âŸ¨f, ğŸ™ _âŸ©

instance pullback_IsHomLift : IsHomLift (Ï€ F) f (pullback_map a f) :=
  -- TODO: rename
  instIsHomLiftMap (Ï€ F) (pullback_map a f)

-- TODO a implicit here?
abbrev pullback_inducedMap {a : F.obj âŸ¨op SâŸ©} (f : R âŸ¶ S) {a' : F.toFibered} (g : a'.1 âŸ¶ R)
    (Ï†' : a' âŸ¶ âŸ¨S, aâŸ©) [IsHomLift (Ï€ F) (g â‰« f) Ï†'] : a' âŸ¶ pullback_obj a f :=
  have : g â‰« f = Ï†'.1 := by simpa using IsHomLift.fac (Ï€ F) (g â‰« f) Ï†'
  âŸ¨g, Ï†'.2 â‰« eqToHom (by simp [this.symm]) â‰« (F.mapComp f.op.toLoc g.op.toLoc).hom.app aâŸ©

instance pullback_inducedMap_isHomLift {a : F.obj âŸ¨op SâŸ©} (f : R âŸ¶ S) {a' : F.toFibered}
    {Ï†' : a' âŸ¶ âŸ¨S, aâŸ©} {g : a'.1 âŸ¶ R} [IsHomLift (Ï€ F) (g â‰« f) Ï†'] :
      IsHomLift (Ï€ F) g (pullback_inducedMap f g Ï†') :=
  instIsHomLiftMap (Ï€ F) (pullback_inducedMap f g Ï†')

lemma pullback_IsPullback : IsStronglyCartesian (Ï€ F) f (pullback_map a f) where
  universal_property' := by
    intros a' g Ï†' hÏ†'
    have : g â‰« f = Ï†'.1 := by simpa using IsHomLift.fac (Ï€ F) (g â‰« f) Ï†'
    use pullback_inducedMap f g Ï†'
    refine âŸ¨âŸ¨inferInstance, ?_âŸ©, ?_âŸ©
    ext
    Â· exact this
    Â· simp
    rintro Ï‡' âŸ¨hÏ‡'.symm, hÏ‡'â‚âŸ©
    subst hÏ‡'â‚
    -- TODO: subst_hom_lift here? Need better version for that ....
    have hgÏ‡' : g = Ï‡'.1 := by simpa using IsHomLift.fac (Ï€ F) g Ï‡'
    subst hgÏ‡'
    ext <;> simp

end

/-- `Ï€` is a fibered category. -/
instance : IsFibered (Ï€ F) := by
  apply IsFibered.of_has_pullbacks'
  intros a R f
  use pullback_obj a.2 f, pullback_map a.2 f
  exact pullback_IsPullback a.2 f

-- section?
variable (F) (S : ğ’®)

@[simps]
def Î¹ : F.obj âŸ¨op SâŸ© â¥¤ F.toFibered where
  obj := fun a => âŸ¨S, aâŸ©
  map := @fun a b Ï† => âŸ¨ğŸ™ S, Ï† â‰« (F.mapId âŸ¨op SâŸ©).inv.app bâŸ©
  map_id := fun a => by ext <;> simp
  map_comp := by
    intro a b c Ï† Ïˆ
    ext
    Â· simp
    dsimp
    conv_rhs =>
      congr; rw [assoc]; congr; rfl
      rw [Functor.map_comp, assoc, â† (F.mapId âŸ¨op SâŸ©).inv.naturality_assoc Ïˆ]
      rw [â† Cat.whiskerRight_app, â† NatTrans.comp_app, F.mapComp_id_left_ofStrict_inv]
      rw [â† assoc (h := eqToHom _), inv_hom_whiskerRight]
    simp


@[simps]
def comp_iso : (Î¹ F S) â‹™ Ï€ F â‰… (const (F.obj âŸ¨op SâŸ©)).obj S where
  hom := { app := fun a => ğŸ™ _ }
  inv := { app := fun a => ğŸ™ _ }

lemma comp_const : (Î¹ F S) â‹™ Ï€ F = (const (F.obj âŸ¨op SâŸ©)).obj S := by
  apply Functor.ext_of_iso (comp_iso F S) <;> simp

noncomputable instance : Functor.Full (Fiber.InducedFunctor (comp_const F S)) where
  map_surjective := by
    intro X Y f
    have hf : f.1.1 = ğŸ™ S := by simpa using (IsHomLift.fac (Ï€ F) (ğŸ™ S) f.1).symm
    use f.1.2 â‰« eqToHom (by simp [hf]) â‰« (F.mapId âŸ¨op SâŸ©).hom.app Y
    ext <;> simp [hf]

instance : Functor.Faithful (Fiber.InducedFunctor (comp_const F S)) where
  map_injective := by
    intros a b f g heq
    -- can be made a one liner...
    rw [â† Subtype.val_inj] at heq
    obtain âŸ¨_, heqâ‚‚âŸ© := (hom_ext_iff _ _).1 heq
    simpa [cancel_mono] using heqâ‚‚

noncomputable instance : Functor.EssSurj (Fiber.InducedFunctor (comp_const F S)) := by
  apply essSurj_of_surj
  intro Y
  have hYS : Y.1.1 = S := by simpa using Y.2
  use (hYS.symm â–¸ Y.1.2)
  apply Subtype.val_inj.1
  apply Sigma.ext <;> simp [hYS]

noncomputable instance : Functor.IsEquivalence (Fiber.InducedFunctor (comp_const F S)) where

noncomputable instance : HasFibers (Ï€ F) where
  Fib S := F.obj âŸ¨op SâŸ©
  Î¹ := Î¹ F
  comp_const := comp_const F

end Pseudofunctor.toFibered

end CategoryTheory
