/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Order.CompletePartialOrder
import Mathlib.Topology.Algebra.Indicator
import Mathlib.Topology.ContinuousMap.Compact
import Mathlib.Topology.Separation.DisjointCover
import Mathlib.Order.Disjointed

/-!
# Uniform approximation by locally constant functions

We show that if `X` is a profinite space, and `V` is a uniform space, then any continuous function
`X â†’ V` can be uniformly approximated by locally constant functions.
-/

open Metric UniformSpace Set

open TopologicalSpace (Opens Clopens IsOpenCover)

open scoped Uniformity Function

namespace ContinuousMap

variable {Î¹ X V : Type*}
  [TopologicalSpace X] [TotallyDisconnectedSpace X] [T2Space X] [CompactSpace X]
  [UniformSpace V] (f : C(X, V)) {S : Set (V Ã— V)}

/--
For any continuous function on `X` and `S âˆˆ ğ“¤ V`, there exists a finite cover of `X` by
pairwise-disjoint nonempty clopens, on each of which `f` varies within `S`.
-/
lemma exists_disjoint_nonempty_clopen_cover_of_mem_uniformity (hS : S âˆˆ ğ“¤ V) :
    âˆƒ (n : â„•) (D : Fin n â†’ Clopens X), (âˆ€ i, D i â‰  âŠ¥) âˆ§
    (âˆ€ i, âˆ€ y âˆˆ D i, âˆ€ z âˆˆ D i, (f y, f z) âˆˆ S) âˆ§ (univ : Set X) âŠ† â‹ƒ i, â†‘(D i) âˆ§
    Pairwise (Disjoint on D) := by
  -- Find a neighbourhood of each point on which `f` varies within `S`
  have step1 (x) : âˆƒ U : Opens X, x âˆˆ U âˆ§ âˆ€ y âˆˆ U, âˆ€ z âˆˆ U, (f y, f z) âˆˆ S := by
    obtain âŸ¨R, hR, hR', hRSâŸ© := comp_symm_of_uniformity hS
    obtain âŸ¨U, hUB, hUo, hUxâŸ© := mem_nhds_iff.mp <|  (f.continuousAt x).preimage_mem_nhds
      <| UniformSpace.ball_mem_nhds _ hR
    exact âŸ¨âŸ¨U, hUoâŸ©, hUx, fun y hy z hz â†¦ hRS <| prod_mk_mem_compRel (hR' <| hUB hy) (hUB hz)âŸ©
  choose U hUx hUS using step1
  have hUc : IsOpenCover U := by ext x; simpa using âŸ¨x, hUx xâŸ©
  -- Now refine it to a disjoint covering.
  obtain âŸ¨n, W, hWâ‚, hWâ‚‚, hWâ‚ƒâŸ© := hUc.exists_finite_nonempty_disjoint_clopen_cover
  refine âŸ¨n, W, fun j â†¦ (hWâ‚ j).1, fun j y hy z hz â†¦ ?_, hWâ‚‚, hWâ‚ƒâŸ©
  exact match (hWâ‚ j).2 with | âŸ¨i, hiâŸ© => hUS i y (hi hy) z (hi hz)

/--
Any continuous function from a profinite space to a uniform space can be uniformly approximated
by a function factoring through `Fin n`, for some `n`.
-/
lemma exists_fin_comp_of_mem_uniformity (hS : S âˆˆ ğ“¤ V) :
    âˆƒ (n : â„•) (g : X â†’ Fin n) (h : Fin n â†’ V), Continuous g âˆ§ âˆ€ x, (f x, h (g x)) âˆˆ S := by
  classical
  obtain âŸ¨n, E, hEne, hES, hEuniv, hEdisâŸ© :=
    exists_disjoint_nonempty_clopen_cover_of_mem_uniformity f hS
  have h_uniq (x) : âˆƒ! i, x âˆˆ E i := by
    refine match Set.mem_iUnion.mp (hEuniv <| mem_univ x) with | âŸ¨i, hiâŸ© => âŸ¨i, hi, fun j hj â†¦ ?_âŸ©
    refine Set.PairwiseDisjoint.elim (fun u _ v _ huv â†¦ hEdis huv)
      (Set.mem_univ j) (Set.mem_univ i) ?_
    simp only [Disjoint, not_forall, le_bot_iff, â† SetLike.coe_set_eq, Clopens.coe_bot,
      â† nonempty_iff_ne_empty]
    exact âŸ¨E i âŠ“ E j, inf_le_right, inf_le_left, x, by simp_allâŸ©
  choose g hg hg' using h_uniq
  have h_ex (i) : âˆƒ x, x âˆˆ E i := by
    simpa [â† SetLike.coe_set_eq, â† nonempty_iff_ne_empty] using hEne i
  choose r hr using h_ex
  refine âŸ¨n, g, f âˆ˜ r, continuous_discrete_rng.mpr fun j â†¦ ?_, fun x â†¦ (hES _) _ (hg _) _ (hr _)âŸ©
  convert (E j).isOpen
  exact Set.ext fun x â†¦ âŸ¨fun hj â†¦ hj â–¸ hg x, fun hx â†¦ (hg' _ _ hx).symmâŸ©

/--
If `f` is a continuous map from a profinite space to a uniform space with a group structure, then
we can uniformly approximate `f` by finite products of indicator functions of clopen sets.
-/
@[to_additive "If `f` is a continuous map from a profinite space to a uniform space with an
additive group structure, then we can uniformly approximate `f` by finite sums of indicator
functions of clopen sets."]
lemma exists_sum_const_mulIndicator_approx [CommGroup V] (hS : S âˆˆ ğ“¤ V) :
    âˆƒ (n : â„•) (U : Fin n â†’ Clopens X) (v : Fin n â†’ V),
    âˆ€ x, (f x, âˆ n, mulIndicator (U n) (fun _ â†¦ v n) x) âˆˆ S := by
  obtain âŸ¨n, g, h, hg, hghâŸ© := exists_fin_comp_of_mem_uniformity f hS
  refine âŸ¨n, fun i â†¦ âŸ¨_, (isClopen_discrete {i}).preimage hgâŸ©, h, fun x â†¦ ?_âŸ©
  convert hgh x
  exact (Fintype.prod_eq_single _ fun i hi â†¦ mulIndicator_of_not_mem hi.symm _).trans
    (mulIndicator_of_mem rfl _)

variable
  {Y : Type*} [TopologicalSpace Y] [CompactSpace Y] [AddCommGroup V] [UniformAddGroup V]
  {R : Type*} [TopologicalSpace R] [MonoidWithZero R] [MulActionWithZero R V]

/-- A continuous function on `X Ã— Y` can be uniformly approximated by functions of the form
`f x â€¢ g y`. -/
lemma exists_sum_smul_approx (f : C(X Ã— Y, V)) (hS : S âˆˆ ğ“¤ V) :
    âˆƒ (n : â„•) (g : Fin n â†’ C(X, R)) (h : Fin n â†’ C(Y, V)),
    âˆ€ x y, (f (x, y), âˆ‘ i, g i x â€¢ h i y) âˆˆ S := by
  have hS' : {(f, g) | âˆ€ y, (f y, g y) âˆˆ S} âˆˆ ğ“¤ C(Y, V) :=
    (ContinuousMap.mem_compactConvergence_entourage_iff _).mpr
      âŸ¨_, _, isCompact_univ, hS, by simp only [Set.mem_univ, true_implies, subset_refl]âŸ©
  obtain âŸ¨n, U, v, hvâŸ© := exists_sum_const_indicator_approx f.curry hS'
  refine âŸ¨n, fun i â†¦ âŸ¨_, (U i).isClopen.continuous_indicator <| continuous_const (y := 1)âŸ©,
    v, fun x y â†¦ ?_âŸ©
  convert hv x y using 2
  simp only [ContinuousMap.sum_apply]
  congr 1 with i
  by_cases hi : x âˆˆ U i <;> simp [hi]

/-- A continuous function on `X Ã— Y` can be uniformly approximated by functions of the form
`f x * g y`. -/
lemma exists_sum_mul_approx {ğ•œ : Type*} [NormedRing ğ•œ] (f : C(X Ã— Y, ğ•œ)) {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ (n : â„•) (g : Fin n â†’ C(X, ğ•œ)) (h : Fin n â†’ C(Y, ğ•œ)),
    â€–f - âˆ‘ i, (g i).comp .fst * (h i).comp .sndâ€– < Îµ := by
  obtain âŸ¨n, g, h, hfgâŸ© := exists_sum_smul_approx (R := ğ•œ) f (Metric.dist_mem_uniformity hÎµ)
  refine âŸ¨n, g, h, ?_âŸ©
  simp only [ContinuousMap.norm_lt_iff _ hÎµ]
  intro (x, y)
  simpa [dist_eq_norm_sub] using hfg x y

end ContinuousMap
